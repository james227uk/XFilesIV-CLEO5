//========================================================================================================================================================================================================
//------------------------------------------------------------------------------------------- GTA X-Files IV (2024) --------------------------------------------------------------------------------------
//---------------------------------------------------------------------------------------------- By james227uk -------------------------------------------------------------------------------------------
//========================================================================================================================================================================================================
//                                                                                Developed using Sanny Builder 4 and CLEO5 Beta.
//========================================================================================================================================================================================================


//========================================================================================================================================================================================================
//      1. SCM Header 
//      Core SCM stats such as mission counts, and initial variable declarations.
//========================================================================================================================================================================================================

//--- Primary header
DEFINE MISSIONS 3
DEFINE MISSION 0 AT @Mission_Initial        // 1. Initialisation Script
DEFINE MISSION 1 AT @Mission_Introduction   // 2. Storymode: Introduction
DEFINE MISSION 2 AT @Mission_Arrival        // 3. Storymode: Arrival

DEFINE EXTERNAL_SCRIPTS 1 // Use -1 in order not to compile AAA script
DEFINE SCRIPT DBGCONS AT @External_DebugConsole

DEFINE UNKNOWN_EMPTY_SEGMENT 0
DEFINE UNKNOWN_THREADS_MEMORY 0

//--- Import extensions & libraries
{$USE CLEO}
{$USE CLEO+}
{$USE NewOpcodes}


//--- Initial variable declarations
var 
    $player1: Player         // gta3sc/SBL analogue of PLAYER_CHAR
    $scplayer: Char          // gta3sc/SBL analogue of PLAYER_ACTOR
    $playerGroup: Group
    $mission_trigger_wait_time: Int = 250
    $flagGamemodeChoice: Int = 0
    $flagMissionInitialCompleted: Int = False
    $flagMissionIntroductionCompleted: Int = False
    $flagMissionArrivalCompleted: Int = False
    $flagDebugConsoleOpen: Int = 0
    $flagDebugMythActive: Int = False
    $flagProsperoHQGatesLocked: Int = False
    $flagFollowerActive: Int = False
    $flagProsperoHQGatesLocked: Int = False
end
const
    logPath = "root:\modloader\a X-Files IV\XFilesIV.log"
    On = 1
    Off = 0
    gmStorymode = 0
    gmFreemode = 1
end

//--- External Script Name Mappings
const
    externalDebugConsole = 0   
end

script_name 'MAIN'
declare_mission_flag {Name:} $ONMISSION
Fs.DeleteFile(logPath)






//========================================================================================================================================================================================================
//      2. Initialise World
//      Initialise the world and set initial properties such as weather.
//========================================================================================================================================================================================================
Streaming.RequestCollision({X:}2544.1604, {Y:}2810.7729)
Streaming.LoadScene({X:}2544.1604, {Y:}2810.7729, {Z:}10.8203)
Streaming.SetAreaVisible({InteriorID:}0)
Clock.SetTimeOfDay({Hour:}0, {Minutes:}0)
Weather.ForceNow({Weather ID:} WeatherType.RainySf)



//========================================================================================================================================================================================================
//      3. Initialise Player
//      Create player and set initial stats.
//======================================================================================================================================================================================================== 

//--- Create player
Player.Create({Index:}0, {X:}2544.1604, {Y:}2810.7729, {Z:}10.8203, {Player:} $player1)
Player.GetChar({Player:} $player1, {Player Char:} $scplayer)
Player.GetGroup({Player:} $player1, {Player Group:} $playerGroup)
Player.SetControl({Player:} $player1, {State:} False)

//--- Set player clothes
Player.GiveClothesOutsideShop({Player:} $player1, {Texture:}"PLAYER_FACE",    {Model:}"HEAD",    {Body Part:}BodyPart.Head)
Player.GiveClothesOutsideShop({Player:} $player1, {Texture:}"JEANSDENIM",     {Model:}"JEANS",   {Body Part:}BodyPart.Legs)
Player.GiveClothesOutsideShop({Player:} $player1, {Texture:}"SNEAKERBINCBLK", {Model:}"SNEAKER", {Body Part:}BodyPart.Shoes)
Player.GiveClothesOutsideShop({Player:} $player1, {Texture:}"VEST",           {Model:}"VEST",    {Body Part:}BodyPart.Torso)
Player.BuildModel($player1)

//--- Set respawn points
Restart.AddHospital({X:}2027.77, {Y:} -1420.52, {Z:}15.99, {Heading:}137.0, {TownID:} 0)
Restart.AddPolice(  {X:}1550.68, {Y:}-1675.49,  {Z:}14.51, {Heading:}90.0,  {TownID:} 0)

//--- Set initial stats
Stat.SetFloat(StatID.MaxHealth,                     {Value:}1000.0)
Stat.SetInt  (StatID.DrivingSkill,                  {Value:}10000 )
Stat.SetInt  (StatID.FlyingSkill,                   {Value:}10000 )
Stat.SetInt  (StatID.BikeSkill,                     {Value:}10000 )
Stat.SetInt  (StatID.CycleSkill,                    {Value:}10000 )
Stat.SetFloat(StatID.WeapontypePistolSkill,         {Value:}1000.0)
Stat.SetFloat(StatID.WeapontypePistolSilencedSkill, {Value:}1000.0)
Stat.SetFloat(StatID.WeapontypeDesertEagleSkill,    {Value:}1000.0)
Stat.SetFloat(StatID.WeapontypeShotgunSkill,        {Value:}1000.0)
Stat.SetFloat(StatID.WeapontypeSawnoffShotgunSkill, {Value:}1000.0)
Stat.SetFloat(StatID.WeapontypeSpas12ShotgunSkill,  {Value:}1000.0)
Stat.SetFloat(StatID.WeapontypeMicroUziSkill,       {Value:}1000.0)
Stat.SetFloat(StatID.WeapontypeMp5Skill,            {Value:}1000.0)
Stat.SetFloat(StatID.WeapontypeAk47Skill,           {Value:}1000.0)
Stat.SetFloat(StatID.WeapontypeM4Skill,             {Value:}1000.0)
Stat.SetFloat(StatID.WeapontypeSniperrifleSkill,    {Value:}1000.0)
Stat.SetInt  (StatID.CitiesPassed,                  {Value:}     4)
Player.IncreaseMaxArmor($player1,                   {Value:}  5000)
Player.AddScore        ($player1,                   {Value:}  5000)
Game.SetMaxWantedLevel (                            {Value:}     6)
Char.SetCanBeKnockedOffBike($scplayer, false)
Game.AllowPauseInWidescreen(True)
set_deatharrest_state 0

//========================================================================================================================================================================================================
//      4. Final Initialisation
//      Final initialisation steps, launch the initial 'mission', then create initial threads.
//========================================================================================================================================================================================================
wait 0

//--- Early Loaders
start_new_script @Debug_DebugConsole
start_new_script @Script_Universal_ProsperoHQ_Gates

//--- Launch the initialisation 'mission'. This contains the main menu etc.
Mission.LoadAndLaunchInternal(Mission_Initial)
Debugger.LogLine({Filename:}logPath, {Timestamp:}True, {Text:}"[Main] [INFO] Launching mission 'initial'...")
$ONMISSION = True
while $flagMissionInitialCompleted == False
    wait 0
end
Debugger.LogLine({Filename:}logPath, {Timestamp:}True, {Text:}"[Main] [INFO] Returned from mission 'initial'.")

// Pick what to do next based on gamemode choice.
if $flagGamemodeChoice == gmStorymode // Storymode
then
    // Launch Introduction cutscene
    wait 0
    Mission.LoadAndLaunchInternal(Mission_Introduction)
    Debugger.LogLine({Filename:}logPath, {Timestamp:}True, {Text:}"[Main] [INFO] Launching mission 'introduction'...")
    $ONMISSION = True
    while $flagMissionIntroductionCompleted == False
        wait 0
    end
    $ONMISSION = False
    Debugger.LogLine({Filename:}logPath, {Timestamp:}True, {Text:}"[Main] [INFO] Returned from mission 'introduction'.")
    
    // Launch 'Arrival' mission    
    wait 0
    Mission.LoadAndLaunchInternal(Mission_Arrival)
    Debugger.LogLine({Filename:}logPath, {Timestamp:}True, {Text:}"[Main] [INFO] Launching mission 'arrival'...")
    $ONMISSION = True
    while $flagMissionArrivalCompleted == False
        wait 0
    end
    $ONMISSION = False
    Debugger.LogLine({Filename:}logPath, {Timestamp:}True, {Text:}"[Main] [INFO] Returned from mission 'arrival'.")    
else
    // Freemode
    Camera.RestoreJumpcut()
    Player.SetControl($player1, True)
    func_giveAgentOutfit()
    Camera.DoFade(1000, Fade.In)
    Hud.SwitchWidescreen(False)
    wait 1000
    Char.SetCoordinates($scplayer, 2590.947, 2790.5645, 10.8203)
    Char.SetHeading($scplayer, 90.0)
end   
//Audio.PlayMissionPassedTune(2) // Debug

//--- Create universal scripts
start_new_script @Script_Universal_HQSavePoint

Zone.SetPopulationType('KACC', 17) // 'GOLF_CLUB' in popcycl.dat
Zone.SetGangStrength('KACC', GangType.Gang9, 100)


Player.SetControl({Player:} $player1, {State:}True)
Camera.RestoreJumpcut()
Hud.SwitchWidescreen(False)


//========================================================================================================================================================================================================
//      5. Main Loop
//      Keep the MAIN script in a perpetual loop to prevent shenanigans.
//========================================================================================================================================================================================================
while true
    wait $mission_trigger_wait_time
    Clock.GetTimeOfDay({Hours Var:} $TIME_HOURS, {Minutes Var:} $TIME_MINS)
end


//========================================================================================================================================================================================================
//      6. Universal Scripts
//      Scripts that run in both Storymode and Freemode.
//========================================================================================================================================================================================================

//------------------------------------------------------------------------------------------------------
//      Universal Scripts | Prospero H.Q. | Save Point
//      - Adds a save pickup at Prospero H.Q. by the front door.
//------------------------------------------------------------------------------------------------------
:Script_Universal_HQSavePoint
script_name 'HQSAVE'
0@ = 2592.0503  // Pickup X
1@ = 2783.4104  // Pickup Y
2@ = 10.9844    // Pickup Z
{
3@: Save Pickup
}

:Script_Universal_HQSavePoint_Loop
World.ClearArea({Pos3:}0@, 1@, 2@, {Radius:}1.0, {Clear Particles:}True)
Pickup.Create(#PICKUPSAVE, PickupType.Once, {Pos3:}0@, 1@, 2@, {Handle:}3@)
repeat
    wait 100
until Pickup.HasBeenCollected(3@)
$ONMISSION = 1
Player.SetControl($player1, False)
Game.ActivateSaveMenu()

repeat
    wait 100
until Game.HasSaveGameFinished()
Debugger.LogLine({Filename:}logPath, {Timestamp:}True, {Text:}"[Main] [INFO] Player saved game successfully.") 
Pickup.Remove(3@)

repeat
    wait 100
until Player.IsPlaying($player1)
Camera.RestoreJumpcut()
Camera.SetBehindPlayer()
Player.SetControl($player1, True)
$ONMISSION = 0

repeat
    wait 0
until not Char.LocateAnyMeans2D($scplayer, {Pos2:}0@, 1@, {Radius Pos2:}2.0, 2.0, {Draw Sphere:}False)
jump @Script_Universal_HQSavePoint_Loop
terminate_this_script



//------------------------------------------------------------------------------------------------------
//      Universal Scripts | Prospero H.Q. | Base Gates
//      - Create the base gates and then open/close them based on proximity.
//------------------------------------------------------------------------------------------------------
:Script_Universal_ProsperoHQ_Gates
script_name 'PHQGATE'
Debugger.LogLine({Filename:}logPath, {Timestamp:}True, {Text:}"[PHQGATE] [INFO] Universal script 'Prospero H.Q. Gates' loaded at 'PHQGATE'.")    

// Create gate objects
Streaming.RequestModel(#GATE_AUTOR)
Streaming.RequestModel(#GATE_AUTOL)
Streaming.LoadAllModelsNow()
0@ = Object.Create(#GATE_AUTOL, {Pos3:}2497.41, 2777.07, 9.8)
1@ = Object.Create(#GATE_AUTOR, {Pos3:}2497.41, 2769.11, 9.8)
Object.SetHeading(0@, 90.0)
Object.SetHeading(1@, 90.0)
Object.SetDynamic(0@, True)
Object.SetDynamic(1@, True)
Streaming.MarkModelAsNoLongerNeeded(#GATE_AUTOR)
Streaming.MarkModelAsNoLongerNeeded(#GATE_AUTOL)

// Reduce polling rate until player is within a decent range of the gates.
:Script_Universal_ProsperoHQ_Gates_Loop_FarRange
wait 5000
gosub @Script_Universal_ProsperoHQ_Gates_Lock
if Char.LocateAnyMeans3D($scplayer, {Pos3:}2497.2947, 2773.2153, 10.8262, {Radius Pos3:}200.0, 200.0, 200.0, {Draw Sphere:}False)
jf @Script_Universal_ProsperoHQ_Gates_Loop_FarRange
Debugger.LogLine({Filename:}logPath, {Timestamp:}True, {Text:}"[PHQGATE] [INFO] Switched to quicker polling rate.")  

:Script_Universal_ProsperoHQ_Gates_Loop_CloseRange
wait 0
gosub @Script_Universal_ProsperoHQ_Gates_Lock
if $flagFollowerActive == True
then
    if or
        Char.LocateAnyMeans3D($scplayer, {Pos3:}2497.2947, 2773.2153, 10.8262, {Radius Pos3:}20.0, 20.0, 20.0, {Draw Sphere:}False)
        Char.LocateAnyMeans3D($cFollower, {Pos3:}2497.2947, 2773.2153, 10.8262, {Radius Pos3:}20.0, 20.0, 20.0, {Draw Sphere:}False)
        then gosub @Script_Universal_ProsperoHQ_Gates_Open
        else gosub @Script_Universal_ProsperoHQ_Gates_Close
    end
else
    if Char.LocateAnyMeans3D($scplayer, {Pos3:}2497.2947, 2773.2153, 10.8262, {Radius Pos3:}20.0, 20.0, 20.0, {Draw Sphere:}False)
        then gosub @Script_Universal_ProsperoHQ_Gates_Open
        else gosub @Script_Universal_ProsperoHQ_Gates_Close
    end
end
if not Char.LocateAnyMeans3D($scplayer, {Pos3:}2497.2947, 2773.2153, 10.8262, {Radius Pos3:}200.0, 200.0, 200.0, {Draw Sphere:}False)
    then jump @Script_Universal_ProsperoHQ_Gates_Loop_FarRange
end
jump @Script_Universal_ProsperoHQ_Gates_Loop_CloseRange

:Script_Universal_ProsperoHQ_Gates_Open
Object.Slide(0@, {From Pos3:}2497.41, 2784.77, 9.8, {Speed Pos3:}0.0, 0.1, 0.0, {Collision Check:}False)
Object.Slide(1@, {From Pos3:}2497.41, 2761.41, 9.8, {Speed Pos3:}0.0, 0.1, 0.0, {Collision Check:}False)
return

:Script_Universal_ProsperoHQ_Gates_Close
Object.Slide(0@, {From Pos3:}2497.41, 2777.07, 9.8, {Speed Pos3:}0.0, 0.1, 0.0, {Collision Check:}False)
Object.Slide(1@, {From Pos3:}2497.41, 2769.11, 9.8, {Speed Pos3:}0.0, 0.1, 0.0, {Collision Check:}False)
return

:Script_Universal_ProsperoHQ_Gates_Lock
if $flagProsperoHQGatesLocked == True
then
    repeat 
        wait 0
    until $flagProsperoHQGatesLocked == False
end
return



//------------------------------------------------------------------------------------------------------
//      Universal Scripts | Debug | Debug Console
//      - A debug development console containing useful tools and readouts.
//------------------------------------------------------------------------------------------------------
:Debug_DebugConsole
script_name 'DBGCONL' 

while true
    wait 0
    if Pad.IsKeyPressed(KeyCode.F6)
    then
        while Pad.IsKeyPressed(KeyCode.F6)
            wait 0
        end 
        func_runExternalScript(externalDebugConsole, "External-DebugConsole")
    end
    
    while $flagDebugConsoleOpen == True
        wait 0
    end
    StreamedScript.MarkAsNoLongerNeeded(externalDebugConsole)
end



//------------------------------------------------------------------------------------------------------
//      Universal Scripts | Debug | Debug Follower
//      - A quick and dirty follower script to test interactions with other scripts.
//------------------------------------------------------------------------------------------------------
:Debug_Follower
script_name 'DBGFLWR'
{   -- Var Mapping --
    0@: Player X
    1@: Player Y
    2@: Player Z
    3@: Proximity Radius
    4@: Blip
}
3@ = 150.0

Streaming.RequestModel(#WFYST)
Streaming.RequestModel(#M4)
Streaming.LoadAllModelsNow()
Char.GetOffsetInWorldCoords($scplayer, 5.0, 0.0, 0.0, 0@, 1@, 2@)
Char $cFollower = Char.Create(PedType.Gang10, #WFYST, 0@, 1@, -100.0)
Char.GiveWeapon($cFollower, 31, 99999)
Char.ShutUp($cFollower, True)
Char.SetSuffersCriticalHits($cFollower, False)
Char.SetProofs($cFollower, True, True, True, True, True)
Group.SetMember($playerGroup, $cFollower)
Char.SetNeverLeavesGroup($cFollower, True)
Char.SetRelationship($cFollower, RelationshipType.Respect, PedType.Player1)
Char.SetRelationship($cFollower, RelationshipType.Like, PedType.Gang9) // Prospero
Char.SetRelationship($cFollower, RelationshipType.Hate, PedType.Gang8) // Myths
$flagFollowerActive = True
Streaming.MarkModelAsNoLongerNeeded(#WFYST)
Streaming.MarkModelAsNoLongerNeeded(#M4)
Blip.AddForChar($cFollower, 4@)
Blip.SetAsFriendly(4@, True)
Blip.SetAlwaysDisplayOnZoomedRadar(4@, True)
Debugger.LogLine({Filename:}logPath, {Timestamp:}True, {Text:}"[DBGFLWR] [INFO] Spawned debug follower.") 

:Debug_Follower_Loop
wait 0
if or
    Player.IsDead($player1)
    Pad.TestCheat("FOLLOW")
then
    Char.RemoveElegantly($cFollower)
    Char.MarkAsNoLongerNeeded($cFollower)
    Blip.Remove(4@)
    wait 1000
    $flagFollowerActive = False
    Debugger.LogLine({Filename:}logPath, {Timestamp:}True, {Text:}"[DBGFLWR] [INFO] Removed debug follower.") 
    terminate_this_script
end

if not Char.LocateAnyMeansChar3D($cFollower, $scplayer, 3@, 3@, 3@, False)
then
    Char.GetOffsetInWorldCoords($scplayer, 5.0, 0.0, 0.0, 0@, 1@, 2@)
    Char.SetCoordinates($cFollower, 0@, 1@, -100.0)
end
jump @Debug_Follower_Loop



//------------------------------------------------------------------------------------------------------
//      Universal Scripts | Debug | Debug Myth
//      - A quick and dirty myth script to test interactions with other scripts.
//------------------------------------------------------------------------------------------------------
:Debug_Myth
script_name 'DBGMYTH'
{   -- Var Mapping --
    0@: Player X
    1@: Player Y
    2@: Player Z
    3@: Myth
    4@: Blip
}

Char.GetOffsetInWorldCoords($scplayer, 5.0, 0.0, 0.0, 0@, 1@, 2@)
Streaming.LoadSpecialCharacter(1, "BIGFOOT")
Streaming.LoadAllModelsNow()
wait 0
Char.Create(PedType.Gang8, #SPECIAL01, 0@, 1@, 2@, 3@)
func_applyMythTemplate(3@)
Task.KillCharOnFoot(3@, $scplayer)
Char.SetAnimGroup(3@, "oldman")
Char.SetDecisionMaker(3@, 2)
Char.SetRelationship(3@, RelationShipType.Hate, PedType.Player1)
Char.SetRelationship(3@, RelationShipType.Hate, PedType.Gang9) // Prospero
Char.SetRelationship(3@, RelationShipType.Hate, PedType.Gang10) // Followers
$flagDebugMythActive = True
Streaming.UnloadSpecialCharacter(1)
Blip.AddForChar(3@, 4@)
Blip.SetAsFriendly(4@, False)
Blip.SetAlwaysDisplayOnZoomedRadar(4@, True)
Debugger.LogLine({Filename:}logPath, {Timestamp:}True, {Text:}"[DBGMYTH] [INFO] Spawned debug myth.") 

:Debug_Myth_Loop
wait 0
if or
    Char.IsDead(3@)
    not Char.LocateAnyMeansChar2D($scplayer, 3@, 500.0, 500.0, False)
    Pad.TestCheat("MYTH")
then
    Char.RemoveElegantly(3@)
    Char.MarkAsNoLongerNeeded(3@)
    Blip.Remove(4@)
    wait 1000
    $flagDebugMythActive = False
    Debugger.LogLine({Filename:}logPath, {Timestamp:}True, {Text:}"[DBGFLWR] [INFO] Removed debug myth.") 
    terminate_this_script
else
    jump @Debug_Myth_Loop
end

    



//========================================================================================================================================================================================================
//      7. Storymode Scripts
//      Scripts that run exclusively in Storymode.
//========================================================================================================================================================================================================

 

//========================================================================================================================================================================================================
//      8. Freemode Scripts
//      Scripts that run exclusively in Freemode.
//========================================================================================================================================================================================================



//========================================================================================================================================================================================================
//      9. Functions
//      Custom functions called extensively to reduce code bloat.
//========================================================================================================================================================================================================

//------------------------------------------------------------------------------------------------------
//      Functions | Run External Script
//      - This function streams an external script, waits for it to be loaded, then starts it safely.
//------------------------------------------------------------------------------------------------------
function func_runExternalScript(targetScript: int, name: string)
    int scriptCount = StreamedScript.GetNumberOfInstances(targetScript)
    if scriptCount == 0
    then
        StreamedScript.Stream(targetScript)
        while not StreamedScript.HasLoaded(targetScript)
            wait 0
        end
        StreamedScript.StartNew(targetScript)
        Debugger.LogLine({Filename:}logPath, {Timestamp:}True, {Text:}"[Main] [INFO] Began external script '%s'.", name)
    else
        Debugger.LogLine({Filename:}logPath, {Timestamp:}True, {Text:}"[Main] [ERROR] Failed to start external script '%s'. Active instances already %i.", name, scriptCount)
    end
end



//------------------------------------------------------------------------------------------------------
//      Functions | Load Model
//      - This function streams a model, waits for it to be loaded, then returns.
//------------------------------------------------------------------------------------------------------
function func_loadModel(modelID: int, forceLoad: int)
    Streaming.RequestModel(modelID)
    if
        forceLoad == 1
    then
        Streaming.LoadAllModelsNow()
        return
    else
        while not Streaming.IsModelAvailable(modelID)
            wait 0
        end
    end
end


//------------------------------------------------------------------------------------------------------
//      Functions | Teleport Player Safely
//      - This function teleports the player safely to the target coordinates, requesting collision beforehand.
//------------------------------------------------------------------------------------------------------
function func_teleportSafe(posX: float, posY: float, heading: float, area: int)
    Streaming.SetAreaVisible(area)
    Char.SetAreaVisible($scplayer, area)
    Char.SetCoordinates($scplayer, posX, posY, 0.0)
    Streaming.RequestCollision(posX, posY)
    Streaming.LoadAllModelsNow()
    wait 0
    
    // put on loaded ground
    Char.SetCoordinates($scplayer, posX, posY, -100.0)
    Char.SetHeading($scplayer, heading)
    
    float posZ
    //Player is already at the waypoint
    Char.GetCoordinates($scplayer, posX, posY, posZ)
    //We're loading the scene
    Streaming.LoadScene(posX, posY, posZ)
    Streaming.LoadAllModelsNow()
end



//------------------------------------------------------------------------------------------------------
//      Functions | Equip Agent Outfit
//      - This function puts CJ in his agent outfit.
//------------------------------------------------------------------------------------------------------
function func_giveAgentOutfit()
    Player.GiveClothesOutsideShop({Player:} $player1, {Texture:}"Player_Face",   {Model:}"HEAD",      {Body Part:}BodyPart.Head)
    Player.GiveClothesOutsideShop({Player:} $player1, {Texture:}"Tuxedo",        {Model:}"Suit2",     {Body Part:}BodyPart.Torso)
    Player.GiveClothesOutsideShop({Player:} $player1, {Texture:}"Suit1TrBlk2",   {Model:}"Suit1Tr",   {Body Part:}BodyPart.Legs)
    Player.GiveClothesOutsideShop({Player:} $player1, {Texture:}"ShoeDressBlk",  {Model:}"Shoe",      {Body Part:}BodyPart.Shoes)
    Player.GiveClothesOutsideShop({Player:} $player1, {Texture:}"WatchCro2",     {Model:}"Watch",     {Body Part:} 14)
    Player.GiveClothesOutsideShop({Player:} $player1, {Texture:}"Glasses05Dark", {Model:}"Glasses03", {Body Part:}BodyPart.Glasses)
    Player.BuildModel($player1)
end



//------------------------------------------------------------------------------------------------------
//      Functions | Equip Casual Outfit
//      - This function puts CJ in his normal casual outfit.
//------------------------------------------------------------------------------------------------------
function func_giveCasualOutfit()
    Player.GiveClothesOutsideShop({Player:} $player1, {Texture:}"Player_Face",    {Model:}"HEAD",   {Body Part:}BodyPart.Head)
    Player.GiveClothesOutsideShop({Player:} $player1, {Texture:}"Tshirtmaddgrey", {Model:}"Tshirt", {Body Part:}BodyPart.Torso)
    Player.GiveClothesOutsideShop({Player:} $player1, {Texture:}"Worktrgrey",     {Model:}"Worktr", {Body Part:}BodyPart.Legs)
    Player.GiveClothesOutsideShop({Player:} $player1, {Texture:}"ShoeDressBlk",   {Model:}"Shoe",   {Body Part:}BodyPart.Shoes)
    Player.GiveClothesOutsideShop({Player:} $player1, {Texture:}0,                {Model:}0,        {Body Part:} 14)
    Player.GiveClothesOutsideShop({Player:} $player1, {Texture:}0,                {Model:}0,        {Body Part:}BodyPart.Glasses)
    Player.BuildModel($player1)
end



//------------------------------------------------------------------------------------------------------
//      Functions | Clear Camera Vector
//      - This function clears stubborn camera vector movements.
//------------------------------------------------------------------------------------------------------
function func_clearCameraVector()
    float cameraX, cameraY, cameraZ
    Camera.GetActiveCoordinates(cameraX, cameraY, cameraZ)
    Camera.SetVectorMove(cameraX, cameraY, cameraZ, cameraX, cameraY, cameraZ, 0, False)
    Camera.SetVectorTrack(cameraX, cameraY, cameraZ, cameraX, cameraY, cameraZ, 0, False)
end



//------------------------------------------------------------------------------------------------------
//      Functions | Give Weapon Safely
//      - This function loads the model before giving the weapon to the specified Char.
//------------------------------------------------------------------------------------------------------
function func_giveWeaponSafely(targetChar: Char, modelID: int, weaponID: int, ammo: int)
    Streaming.RequestModel(modelID)
    Streaming.LoadAllModelsNow()
    Char.GiveWeapon(targetChar, weaponID, ammo)
    Char.SetCurrentWeapon(targetChar, weaponID)
    Streaming.MarkModelAsNoLongerNeeded(modelID)
end



//------------------------------------------------------------------------------------------------------
//      Functions | Play SCM Sound Safely
//      - This function safely loads and then plays an SCM sound.
//------------------------------------------------------------------------------------------------------
function func_playSoundSafely(slotID: int, soundID: int, waitUntilFinished: int)
    Audio.LoadMissionAudio(slotID, soundID)
    repeat
        wait 0
    until Audio.HasMissionAudioLoaded(slotID)
    Audio.PlayMissionAudio(slotID)
    
    if waitUntilFinished == False
    then
        return
    else
        repeat
            wait 0
        until Audio.HasMissionAudioFinished(slotID)
    end
end



//------------------------------------------------------------------------------------------------------
//      Functions | Load AudioStream safely.
//      - This function requests an AudioStream and check's its loaded before playing.
//------------------------------------------------------------------------------------------------------
function func_playAudioStreamSafely(streamID: AudioStream, soundPath: string, waitUntilFinished: int)
    repeat
        wait 0
    until streamID = AudioStream.Load(soundPath)
    AudioStream.SetState(streamID, AudioStreamAction.Play)
    
    if waitUntilFinished == False
    then
        return
    else
        repeat
            wait 0
        until not AudioStream.IsPlaying(streamID)
    end
end



//------------------------------------------------------------------------------------------------------
//      Functions | Play Dialogue
//      - 
//------------------------------------------------------------------------------------------------------
function func_playDialogue(mode: int, asPath: string, scmSoundID: int, subID: string, doAnims: int, forChar: Char, waitUntilFinished: int)
int streamLength = 0
int subtitleDisplayTime = 0
AudioStream streamID

    if mode == 0 // AudioStream mode.
    then
        repeat
            wait 0
        until AudioStream.Load(asPath, streamID)
        AudioStream.GetLength(streamID, streamLength)
        streamLength *= 1000
        streamLength += 1000
        AudioStream.SetState(streamID, AudioStreamAction.Play)
        Text.PrintNow(subID, streamLength, 1)  
        
        if doAnims == True
        then
            Task.PlayAnim({Char:}forChar, {Anim Name:}"idle_chat", {Anim File:}"ped", {Speed:}4.0, {Loop:}False, {LockX:}False, {LockY:}False, {Freeze Frame:}False, {Duration:}streamLength)
            Char.StartFacialTalk(forChar, 10)
        end
        
        if waitUntilFinished == True
        then
            repeat
                wait 0
            until not AudioStream.IsPlaying(streamID)
            if Char.DoesExist(forChar)
            then Char.StopFacialTalk(forChar)
            end
        end 
    end
end



//------------------------------------------------------------------------------------------------------
//      Functions | Restore Camera
//      - This function resets the camera from cutscene mode back to normal mode
//------------------------------------------------------------------------------------------------------
function func_restoreCamera()
    Camera.ResetNewScriptables()
    Camera.RestoreJumpcut()
    Camera.DoFade(0, Fade.In)
    Hud.SwitchWidescreen(False)
    Text.ClearThisPrintBigNow(TextStyle.BottomRight)
end



//------------------------------------------------------------------------------------------------------
//      Functions | Basic Myth Setup
//      - This function applies 'core' properties to myths. Override and extend as required.
//------------------------------------------------------------------------------------------------------
function func_applyMythTemplate(mythChar: Char)
    mythChar.SetDropsWeaponsWhenDead(False)
    mythChar.SetSuffersCriticalHits(False)
    mythChar.SetHealth(20000)
    mythChar.SetMoney(0)
    mythChar.SetUsesUpperbodyDamageAnimsOnly(True)
    mythChar.ShutUp(True)
    mythChar.SetDrownsInWater(False)
    mythChar.SetDecisionMaker(2)
    mythChar.SetRelationship(4, 0)  // Myth hates player
    mythChar.SetRelationship(4, 16) // Myth hates GANG10 (Followers, Prospero etc)
end



//------------------------------------------------------------------------------------------------------
//      Functions | Spawn In Car
//      - This function spawns a car and puts the player in the driver seat. Output handle is returned.
//------------------------------------------------------------------------------------------------------
function func_spawnInCar(targetChar: Char, carModel: Int): int
    float posX, posY, posZ   
    
    Streaming.RequestModel(carModel)
    Streaming.LoadAllModelsNow()
    Char.GetCoordinates(targetChar, {Pos3:}posX, posY, posZ)
    Path.GetClosestCarNode({Input Pos3:}posX, posY, posZ, {Output Pos3:}posX, posY, posZ)
    
    Car c = Car.Create(carModel, {Pos3:}posX, posY, posZ)
    Char.WarpIntoCar(targetChar, c)
    return c
end



//========================================================================================================================================================================================================
//      10. Missions
//      All missions go here. They are only loaded into memory when called.
//========================================================================================================================================================================================================

//------------------------------------------------------------------------------------------------------
//      Missions | Mission 0: Initial
//      - Initialisation 'mission' which has the main menu etc.
//------------------------------------------------------------------------------------------------------
:Mission_Initial
{$INCLUDE_ONCE main\missions\Mission-Initial.txt}

//------------------------------------------------------------------------------------------------------
//      Missions | Mission 1: Introduction
//      - Intro cinematic where the Bloodpool opens and CJ is at the bar.
//------------------------------------------------------------------------------------------------------
:Mission_Introduction
{$INCLUDE_ONCE main\missions\Mission-Introduction.txt}

//------------------------------------------------------------------------------------------------------
//      Missions | Mission 2: Arrival
//      - CJ goes to Prospero H.Q. and is given his first task.
//------------------------------------------------------------------------------------------------------
:Mission_Arrival
{$INCLUDE_ONCE main\missions\Mission-Arrival.txt}

//========================================================================================================================================================================================================
//      11. External Scripts
//      All external scripts go here. They are only loaded into memory when called.
//========================================================================================================================================================================================================

//------------------------------------------------------------------------------------------------------
//      External Scripts | Debug Console
//      - A debug developer console containing useful tools and readouts.
//------------------------------------------------------------------------------------------------------
:External_DebugConsole
{$INCLUDE_ONCE main\externals\External-DebugConsole.txt}
