//========================================================================================================================================================================================================
//------------------------------------------------------------------------------------------- GTA X-Files IV (2024) --------------------------------------------------------------------------------------
//---------------------------------------------------------------------------------------------- By james227uk -------------------------------------------------------------------------------------------
//========================================================================================================================================================================================================
//                                                                                Developed using Sanny Builder 4 and CLEO5 Beta.
//========================================================================================================================================================================================================


//========================================================================================================================================================================================================
//      1. SCM Header 
//      Core SCM stats such as mission counts, and initial variable declarations.
//========================================================================================================================================================================================================

//--- Primary header
DEFINE MISSIONS 4
DEFINE MISSION 0 AT @Mission_Initial        // 1. Initialisation Script
DEFINE MISSION 1 AT @Mission_Introduction   // 2. Storymode: Introduction
DEFINE MISSION 2 AT @Mission_Arrival        // 3. Storymode: Arrival
DEFINE MISSION 3 AT @Mission_FindingTheYeti // 4. Storymode: Finding The Yeti

DEFINE EXTERNAL_SCRIPTS 8
 // Use -1 in order not to compile AAA script
DEFINE SCRIPT DBGCONS AT @External_DebugConsole         // 0
DEFINE SCRIPT HQAGTES AT @External_HQAgentsExterior     // 1
DEFINE SCRIPT HQAGTIS AT @External_HQAgentsInterior     // 2
DEFINE SCRIPT HQWPNVS AT @External_HQWeaponsVendor      // 3
DEFINE SCRIPT HQSETCS AT @External_HQSetpieceCustodian  // 4
DEFINE SCRIPT HQSARAS AT @External_HQSarah              // 5
DEFINE SCRIPT FLWRSWS AT @External_FollowerSarah        // 6
DEFINE SCRIPT TPNETWS AT @External_TPNetwork            // 7

DEFINE UNKNOWN_EMPTY_SEGMENT 0
DEFINE UNKNOWN_THREADS_MEMORY 0

//--- Import extensions & libraries
{$USE CLEO}
{$USE CLEO+}
{$USE NewOpcodes}


//--- Initial variable declarations
var 
    $player1: Player         // gta3sc/SBL analogue of PLAYER_CHAR
    $scplayer: Char          // gta3sc/SBL analogue of PLAYER_ACTOR
    $playerGroup: Group
    $playerArea: Int
    $mission_trigger_wait_time: Int = 250
    $flagGamemodeChoice: Int = 0
    
    $flagMissionInitialCompleted: Int = False
    $flagMissionIntroductionCompleted: Int = False
    $flagMissionArrivalCompleted: Int = False
    $flag_Storymode_Mission_FindingTheYeti_Completed: Int = False
    
    $flag_TPNetwork_Finished: Int = False
    $flagDebugConsoleOpen: Int = False
    $flagDebugMythActive: Int = False
    
    $flagProsperoHostile: Int = False
    $flagProsperoHQGatesLocked: Int = False
    $flag_ProsperoHQ_GuardsEnabled: Int = True
    $flagWeaponsVendorMenuOpen: Int = False
    $flagRelationshipScoreSarah: Int = 0
    $flagIntroducedSarah: Int = False
    $flagGamestage: Int = 0
    $flagFollowerActive: Int = 0
    $flagFollowerSarahAvailable: Int = False
    $flagUpgradeFollowerBonusHealth: Int = 0   
    $flagFacilityLilProbeInnPurchased: Int = False
end
const
    logPath = "root:\modloader\a X-Files IV\XFilesIV.log"
    gmStorymode = 0
    gmFreemode = 1
    followerNone = 0
    followerSarah = 1
end

//--- External Script Name Mappings
const
    externalDebugConsole = 0   
    externalHQAgentsExterior = 1
    externalHQAgentsInterior = 2
    externalHQWeaponsVendor = 3
    externalHQSetpieceCustodian = 4
    externalHQSarah = 5 
    externalFollowerSarah = 6 
    externalTPNetwork = 7           
end 

//--- Initial Weapons Vendor weapon Costs
var
    $iWeaponPricePistol:            Int = 0
    $iWeaponPriceSilencedPistol:    Int = 600
    $iWeaponPriceDesertEagle:       Int = 1200
    $iWeaponPriceTec9:              Int = 360
    $iWeaponPriceUzi:               Int = 600
    $iWeaponPriceMP5:               Int = 2000
    $iWeaponPriceAK47:              Int = 4200
    $iWeaponPriceM4:                Int = 5400
    $iWeaponPriceRifle:             Int = 1200
    $iWeaponPriceSniperRifle:       Int = 5000 
    $iWeaponPriceShotgun:           Int = 720
    $iWeaponPriceSawnOff:           Int = 960
    $iWeaponPriceCombatShotgun:     Int = 1200
    $iWeaponPriceRPG:               Int = 10000
    $iWeaponPriceMissileLauncher:   Int = 15000
    $iWeaponPriceFlamethrower:      Int = 10000 
    $iWeaponPriceMinigun:           Int = 20000
    $iWeaponPriceGrenades:          Int = 2000
    $iWeaponPriceSatchels:          Int = 2000
    $iWeaponPriceMolotovs:          Int = 1000
    $iWeaponPriceTearGas:           Int = 1000
    $iWeaponPriceBrassKnuckles:     Int = 100
    $iWeaponPriceNightStick:        Int = 200
    $iWeaponPriceKnife:             Int = 500
    $iWeaponPriceBaseballBat:       Int = 100
    $iWeaponPriceKatana:            Int = 2000
    $iWeaponPriceChainsaw:          Int = 4000
    $iWeaponPriceArmour:            Int = 1000
    $iWeaponPriceCamera:            Int = 5
    $iWeaponPriceNVGoggles:         Int = 10000
    $iWeaponPriceIRGoggles:         Int = 12000
    $iWeaponPriceParachute:         Int = 1000
    $iWeaponPriceJetpack:           Int = 50000
end
script_name 'MAIN'
declare_mission_flag {Name:} $ONMISSION
Fs.DeleteFile(logPath)



//========================================================================================================================================================================================================
//      2. Initialise World
//      Initialise the world and set initial properties such as weather.
//========================================================================================================================================================================================================
Streaming.RequestCollision({X:}2544.1604, {Y:}2810.7729)
Streaming.LoadScene({X:}2544.1604, {Y:}2810.7729, {Z:}10.8203)
Streaming.SetAreaVisible({InteriorID:}0)
Clock.SetTimeOfDay({Hour:}0, {Minutes:}0)
Weather.ForceNow({Weather ID:} WeatherType.RainySf)

Game.SetRelationship(RelationShipType.Respect, PedType.Gang9, PedType.Gang10) // Prospero respects followers
Game.SetRelationship(RelationShipType.Respect, PedType.Gang10, PedType.Gang9) // Followers respect Prospero.



//========================================================================================================================================================================================================
//      3. Initialise Player
//      Create player and set initial stats.
//======================================================================================================================================================================================================== 

//--- Create player
Player.Create({Index:}0, {X:}2544.1604, {Y:}2810.7729, {Z:}10.8203, {Player:} $player1)
Player.GetChar({Player:} $player1, {Player Char:} $scplayer)
Player.GetGroup({Player:} $player1, {Player Group:} $playerGroup)
Player.SetControl({Player:} $player1, {State:} False)

//--- Set player clothes
Player.GiveClothesOutsideShop({Player:} $player1, {Texture:}"PLAYER_FACE",    {Model:}"HEAD",    {Body Part:}BodyPart.Head)
Player.GiveClothesOutsideShop({Player:} $player1, {Texture:}"JEANSDENIM",     {Model:}"JEANS",   {Body Part:}BodyPart.Legs)
Player.GiveClothesOutsideShop({Player:} $player1, {Texture:}"SNEAKERBINCBLK", {Model:}"SNEAKER", {Body Part:}BodyPart.Shoes)
Player.GiveClothesOutsideShop({Player:} $player1, {Texture:}"VEST",           {Model:}"VEST",    {Body Part:}BodyPart.Torso)
Player.BuildModel($player1)

//--- Set respawn points
Restart.AddHospital({Pos3:}2589.7759, 2790.8264, 10.8203, {Heading:}90.0, {TownID:} 0)
Restart.AddPolice  ({Pos3:}2589.7759, 2790.8264, 10.8203, {Heading:}90.0, {TownID:} 0)


//--- Set initial stats
Stat.SetFloat(StatID.MaxHealth,                     {Value:}1000.0)
Stat.SetFloat(StatID.Respect,                       {Value:}1000.0)
Stat.SetInt  (StatID.DrivingSkill,                  {Value:}10000 )
Stat.SetInt  (StatID.FlyingSkill,                   {Value:}10000 )
Stat.SetInt  (StatID.BikeSkill,                     {Value:}10000 )
Stat.SetInt  (StatID.CycleSkill,                    {Value:}10000 )
Stat.SetFloat(StatID.WeapontypePistolSkill,         {Value:}1000.0)
Stat.SetFloat(StatID.WeapontypePistolSilencedSkill, {Value:}1000.0)
Stat.SetFloat(StatID.WeapontypeDesertEagleSkill,    {Value:}1000.0)
Stat.SetFloat(StatID.WeapontypeShotgunSkill,        {Value:}1000.0)
Stat.SetFloat(StatID.WeapontypeSawnoffShotgunSkill, {Value:}1000.0)
Stat.SetFloat(StatID.WeapontypeSpas12ShotgunSkill,  {Value:}1000.0)
Stat.SetFloat(StatID.WeapontypeMicroUziSkill,       {Value:}1000.0)
Stat.SetFloat(StatID.WeapontypeMp5Skill,            {Value:}1000.0)
Stat.SetFloat(StatID.WeapontypeAk47Skill,           {Value:}1000.0)
Stat.SetFloat(StatID.WeapontypeM4Skill,             {Value:}1000.0)
Stat.SetFloat(StatID.WeapontypeSniperrifleSkill,    {Value:}1000.0)
Stat.SetInt  (StatID.CitiesPassed,                  {Value:}     4)
Player.IncreaseMaxArmor($player1,                   {Value:}  5000)
Player.AddScore        ($player1,                   {Value:} 10000)
Game.SetMaxWantedLevel (                            {Value:}     0)
Char.SetCanBeKnockedOffBike($scplayer, false)
Game.AllowPauseInWidescreen(True)
set_deatharrest_state 0



//========================================================================================================================================================================================================
//      4. Final Initialisation
//      Final initialisation steps, launch the initial 'mission', then create initial threads.
//========================================================================================================================================================================================================

//--- Early Loaders
Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|_______] [INFO] Starting early loaders...") 
start_new_script @Debug_DebugConsole
start_new_script @Script_Universal_ProsperoHQ_Gates

//--- Launch the initialisation 'mission'. This contains the main menu etc.
Mission.LoadAndLaunchInternal(Mission_Initial)
Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|_______] [INFO] Launching mission 'initial'...")
$ONMISSION = True
while $flagMissionInitialCompleted == False
    wait 0
end
Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|_______] [INFO] Returned from mission 'initial'.")

// Pick what to do next based on gamemode choice.
if $flagGamemodeChoice == gmStorymode // Storymode
then
    // Launch Introduction cutscene
    Mission.LoadAndLaunchInternal(Mission_Introduction)
    Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|_______] [INFO] Launching mission 'introduction'...")
    $ONMISSION = True
    while $flagMissionIntroductionCompleted == False
        wait 0
    end
    Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|_______] [INFO] Returned from mission 'introduction'.")
    
    // Launch 'Arrival' mission
    start_new_script @Script_Storymode_Arrival_Loader
    Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|_______] [INFO] Holding until mission 'arrival' is completed.")
    repeat
        wait 0
    until $flagMissionArrivalCompleted == True
    Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|_______] [INFO] Mission 'arrival' completed. Resuming.")
else
    // Freemode
    $flagFollowerSarahAvailable = True
    Camera.RestoreJumpcut()
    Player.SetControl($player1, True)
    func_giveAgentOutfit()
    Text.ClearPrints()
    Text.ClearHelp()
    Camera.DoFade(1000, Fade.In)
    Hud.SwitchWidescreen(False)
    Hud.Display(True)
    Char.SetCoordinates($scplayer, 2590.947, 2790.5645, -100.0)
    Char.SetHeading($scplayer, 90.0)

    Clock.SetTimeOfDay(12, 0)
    
    $flagDebugMythMarkers = True
    
    start_new_script @Script_Freemode_Leatherface
    
end   

//--- Create universal scripts
Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|_______] [INFO] Starting universal scripts...") 
start_new_script @Script_Universal_ProsperoHQ_ExtMasterLoader
start_new_script @Script_Universal_ProsperoHQ_MasterLoader
start_new_script @Script_Universal_ProsperoHQ_Agents_Interior_Loader
start_new_script @Script_Universal_Anomaly_Mimic
start_new_script @Script_Universal_Anomaly_Chrono
start_new_script @Script_Universal_Facility_LilProbeInn
start_new_script @Script_Universal_Facility_MissionaryHill
start_new_script @Script_Universal_TPNetwork_Loader
start_new_script @Script_Debug_PartyBus

Player.SetControl({Player:} $player1, {State:}True)
Camera.RestoreJumpcut()
Hud.SwitchWidescreen(False)


//========================================================================================================================================================================================================
//      5. Main Loop
//      Keep the MAIN script in a perpetual loop to prevent shenanigans.
//========================================================================================================================================================================================================
while true
    wait $mission_trigger_wait_time
    Clock.GetTimeOfDay({Hours Var:} $TIME_HOURS, {Minutes Var:} $TIME_MINS)
    Char.GetAreaVisible($scplayer, $playerArea)
end


//========================================================================================================================================================================================================
//      6. Universal Scripts
//      Scripts that run in both Storymode and Freemode.
//========================================================================================================================================================================================================

//------------------------------------------------------------------------------------------------------
//      Universal Scripts | Prospero H.Q. | Exterior Master Loader
//      - Loads HQ-focused scripts when the player is in close proximity.
//------------------------------------------------------------------------------------------------------

:Script_Universal_ProsperoHQ_ExtMasterLoader
script_name 'HQEXMSL'
Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|HQEXMSL] [INFO] Script started.")  
0@ = True // First Run

while true
    wait 0
    if Char.LocateAnyMeans2D($scplayer, {Pos2:}2618.0, 2724.0, {Rad. Pos2:}300.0, 300.0, {Draw Sphere:}False)
    then
        start_new_script @Script_Universal_HQSavePoint
        start_new_script @Script_Universal_ProsperoHQ_Agents_External_Loader
        start_new_script @Script_Universal_ProsperoHQ_Snipers
        if 0@ == True // If this is the first run, then do nothing.
        then Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|HQEXMSL] [INFO] First run detected, skipping starting some scripts.")  
        else // Else, start the following scripts just like those above.
            start_new_script @Script_Universal_ProsperoHQ_Gates
        end
    end
    0@ = False // No longer first run.    
    repeat
        wait $mission_trigger_wait_time
    until not Char.LocateAnyMeans2D($scplayer, {Pos2:}2618.0, 2724.0, {Rad. Pos2:}300.0, 300.0, {Draw Sphere:}False)
    wait 500
end
//
//
//
//------------------------------------------------------------------------------------------------------
//      Universal Scripts | Prospero H.Q. | Save Point
//      - Adds a save pickup at Prospero H.Q. by the front door
//------------------------------------------------------------------------------------------------------
:Script_Universal_HQSavePoint
script_name 'HQSAVEP'
Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|HQSAVEP] [INFO] Script started.")  
0@ = 2592.0503  // Pickup X
1@ = 2783.4104  // Pickup Y
2@ = 10.9844    // Pickup Z
// 3@: Save Pickup
World.ClearArea({Pos3:}0@, 1@, 2@, {Radius:}1.0, {Clear Particles:}True)
Pickup.Create(#PICKUPSAVE, PickupType.Once, {Pos3:}0@, 1@, 2@, {Handle:}3@)

:Script_Universal_HQSavePoint_Loop
wait 100
if not Char.LocateAnyMeans2D($scplayer, {Pos2:}2618.0, 2724.0, {Rad. Pos2:}300.0, 300.0, {Draw Sphere:}False)
then 
    Pickup.Remove(0@)
    Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|HQSAVEP] [INFO] Script ending (Reason: Player left range of H.Q.).")   
    terminate_this_script
end

if not Pickup.HasBeenCollected(3@)
then jump @Script_Universal_HQSavePoint_Loop
end

$ONMISSION = 1
Player.SetControl($player1, False)
Game.ActivateSaveMenu()

repeat
    wait 100
until Game.HasSaveGameFinished()
Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|HQSAVEP] [INFO] Player saved game successfully.") 
Pickup.Remove(3@)

repeat
    wait 100
until Player.IsPlaying($player1)
Camera.RestoreJumpcut()
Camera.SetBehindPlayer()
Player.SetControl($player1, True)
$ONMISSION = 0

repeat
    wait 0
until not Char.LocateAnyMeans2D($scplayer, {Pos2:}0@, 1@, {Radius Pos2:}2.0, 2.0, {Draw Sphere:}False)


jump @Script_Universal_HQSavePoint_Loop
terminate_this_script
//
//
//
//------------------------------------------------------------------------------------------------------
//      Universal Scripts | Prospero H.Q. | Base Gates
//      - Create the base gates and then open/close them based on proximity.
//------------------------------------------------------------------------------------------------------
:Script_Universal_ProsperoHQ_Gates
script_name 'PHQGATE'
Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|PHQGATE] [INFO] Script started.")   

// Create gate objects
Streaming.RequestModel(#GATE_AUTOR)
Streaming.RequestModel(#GATE_AUTOL)
repeat
    wait 0
    if and
        Streaming.HasModelLoaded(#GATE_AUTOR)
        Streaming.HasModelLoaded(#GATE_AUTOL)
    then Break
    end
until False
0@ = Object.Create(#GATE_AUTOL, {Pos3:}2497.41, 2777.07, 9.8)
1@ = Object.Create(#GATE_AUTOR, {Pos3:}2497.41, 2769.11, 9.8)
Object.SetHeading(0@, 90.0)
Object.SetHeading(1@, 90.0)
Object.SetDynamic(0@, True)
Object.SetDynamic(1@, True)
Streaming.MarkModelAsNoLongerNeeded(#GATE_AUTOR)
Streaming.MarkModelAsNoLongerNeeded(#GATE_AUTOL)

:Script_Universal_ProsperoHQ_Gates_Loop_CloseRange
wait 0
if not Char.LocateAnyMeans2D($scplayer, {Pos2:}2618.0, 2724.0, {Rad. Pos2:}300.0, 300.0, {Draw Sphere:}False)
then
    Object.Delete(0@)
    Object.Delete(1@)
    Object.MarkAsNoLongerNeeded(0@)
    Object.MarkAsNoLongerNeeded(1@)
    Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|PHQGATE] [INFO] Script ending (Reason: Player left range of H.Q.).")  
    terminate_this_script
end

gosub @Script_Universal_ProsperoHQ_Gates_Lock
if and
    $flagFollowerActive > 0
    Char.DoesExist($cFollower)
then
    if or
        Char.LocateAnyMeans3D($scplayer, {Pos3:}2497.2947, 2773.2153, 10.8262, {Radius Pos3:}20.0, 20.0, 20.0, {Draw Sphere:}False)
        Char.LocateAnyMeans3D($cFollower, {Pos3:}2497.2947, 2773.2153, 10.8262, {Radius Pos3:}20.0, 20.0, 20.0, {Draw Sphere:}False)
        then gosub @Script_Universal_ProsperoHQ_Gates_Open
        else gosub @Script_Universal_ProsperoHQ_Gates_Close
    end
else
    if Char.LocateAnyMeans3D($scplayer, {Pos3:}2497.2947, 2773.2153, 10.8262, {Radius Pos3:}20.0, 20.0, 20.0, {Draw Sphere:}False)
        then gosub @Script_Universal_ProsperoHQ_Gates_Open
        else gosub @Script_Universal_ProsperoHQ_Gates_Close
    end
end
jump @Script_Universal_ProsperoHQ_Gates_Loop_CloseRange

:Script_Universal_ProsperoHQ_Gates_Open
Object.Slide(0@, {From Pos3:}2497.41, 2784.77, 9.8, {Speed Pos3:}0.0, 0.1, 0.0, {Collision Check:}False)
Object.Slide(1@, {From Pos3:}2497.41, 2761.41, 9.8, {Speed Pos3:}0.0, 0.1, 0.0, {Collision Check:}False)
return

:Script_Universal_ProsperoHQ_Gates_Close
Object.Slide(0@, {From Pos3:}2497.41, 2777.07, 9.8, {Speed Pos3:}0.0, 0.1, 0.0, {Collision Check:}False)
Object.Slide(1@, {From Pos3:}2497.41, 2769.11, 9.8, {Speed Pos3:}0.0, 0.1, 0.0, {Collision Check:}False)
return

:Script_Universal_ProsperoHQ_Gates_Lock
if $flagProsperoHQGatesLocked == True
then
    repeat 
        wait $mission_trigger_wait_time
    until $flagProsperoHQGatesLocked == False
end
return
terminate_this_script
//
//
//
//------------------------------------------------------------------------------------------------------
//      Universal Scripts | Prospero H.Q. | Snipers
//      - This script creates snipers on the guard towers at Prospero H.Q.
//------------------------------------------------------------------------------------------------------
:Script_Universal_ProsperoHQ_Snipers
script_name 'HQSNIPR'
Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|HQSNIPR] [INFO] Script started.")

while true
    wait 0
    if $flag_ProsperoHQ_GuardsEnabled == False
    then Break
    end
    
    if and
        Streaming.HasModelLoaded(#BMYMIB)
        Streaming.HasModelLoaded(#WMOMIB)
        Streaming.HasModelLoaded(#SNIPER)
    then
        // Create snipers
        0@ = localFunc_createSniper(#BMYMIB, {Pos2:}2501.8257, 2786.8713, {Heading:}134.4933)
        1@ = localFunc_createSniper(#WMOMIB, {Pos2:}2746.5903, 2854.3540, {Heading:}311.4819)
        2@ = localFunc_createSniper(#BMYMIB, {Pos2:}2746.7422, 2668.1873, {Heading:}227.2174) 
        3@ = localFunc_createSniper(#BMYMIB, {Pos2:}2698.2397, 2620.4932, {Heading:}219.6978)
        4@ = localFunc_createSniper(#WMOMIB, {Pos2:}2572.0151, 2620.3350, {Heading:}137.9170) 
        Streaming.MarkModelAsNoLongerNeeded(#BMYMIB)
        Streaming.MarkModelAsNoLongerNeeded(#WMOMIB)
        Streaming.MarkModelAsNoLongerNeeded(#SNIPER)    
        
        // Main Loop
        while true
            wait 0
            // Check player is still in range, else end script.
            if not Char.LocateAnyMeans2D($scplayer, {Pos2:}2618.0, 2724.0, {Rad. Pos2:}300.0, 300.0, {Draw Sphere:}False)
            then
                Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|HQSNIPR] [INFO] Script ending (Reason: Player left range of H.Q.).")
                Break
            end
            
            // Check if snipers should be hostile to player.
            if or
                $flagProsperoHostile == True
                Char.HasBeenDamagedByChar(0@, $scplayer)
                Char.HasBeenDamagedByChar(1@, $scplayer)
                Char.HasBeenDamagedByChar(2@, $scplayer)
                Char.HasBeenDamagedByChar(3@, $scplayer)
                Char.HasBeenDamagedByChar(4@, $scplayer)
            then
                $flagProsperoHostile = True
                Task.KillCharOnFoot(0@, $scplayer)
                Task.KillCharOnFoot(1@, $scplayer)
                Task.KillCharOnFoot(2@, $scplayer)
                Task.KillCharOnFoot(3@, $scplayer)
                Task.KillCharOnFoot(4@, $scplayer)
            end
        end
        Break             
    else
        Streaming.RequestModel(#BMYMIB)
        Streaming.RequestModel(#WMOMIB)
        Streaming.RequestModel(#SNIPER)
    end
end
terminate_this_script

function localFunc_createSniper(model: Int, posX: Float, posY: Float, heading: Float): Int
    Char outputChar
    outputChar = Char.Create(PedType.Gang9, model, posX, posY, -100.0)
    outputChar.SetHeading(heading)
    outputChar.SetHealth(500)
    outputChar.GiveWeapon(WeaponType.Sniper, 250)
    outputChar.SetAccuracy(100)
    outputChar.SetWeaponSkill(WeaponSkill.Pro)
    outputChar.SetStayInSamePlace(True)
    return outputChar
end
//
//
//
//------------------------------------------------------------------------------------------------------
//      Universal Scripts | Prospero H.Q. | External Agents (Loader)
//      - Creates a random assortmment of agents roaming the H.Q. grounds. Loader script.
//------------------------------------------------------------------------------------------------------
:Script_Universal_ProsperoHQ_Agents_External_Loader
script_name 'HQAGTEL'
Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|HQAGTEL] [INFO] Script started.")  

while true
    wait $mission_trigger_wait_time
    if Char.LocateAnyMeans3D($scplayer, {Pos3:}2497.2947, 2773.2153, 10.8262, 300.0, 300.0, 300.0, False)
    then
        func_runExternalScript(externalHQAgentsExterior, "External-HQAgentsExterior")
        repeat
            wait 1000
        until not Char.LocateAnyMeans3D($scplayer, {Pos3:}2497.2947, 2773.2153, 10.8262, 300.0, 300.0, 300.0, False) 
        Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|HQAGTEL] [INFO] Script ending (Reason: Player left range of H.Q.).")  
        StreamedScript.MarkAsNoLongerNeeded(externalHQAgentsExterior)
        terminate_this_script
    end
end
terminate_this_script



//------------------------------------------------------------------------------------------------------
//      Universal Scripts | Prospero H.Q. | Master Loader (Interior)
//      - This script launches Prospero H.Q. interior scripts when the player enters the interior.
//------------------------------------------------------------------------------------------------------
:Script_Universal_ProsperoHQ_MasterLoader
script_name 'HQMASTL'
Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|HQMASTL] [INFO] Script started.")  

while true
    wait 0
    if $playerArea == 3
    then
        Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|HQMASTL] [INFO] H.Q. interior entered. Starting subscripts...")  
        start_new_script @Script_Universal_ProsperoHQ_WeaponsVendor_Loader
        start_new_script @Script_Universal_ProsperoHQ_CustodianSetPiece_Loader 
        start_new_script @Script_Universal_ProsperoHQ_Sarah_Loader       
        repeat
            wait 0
        until not $playerArea == 3
    end
end
terminate_this_script
//
//
//
//------------------------------------------------------------------------------------------------------
//      Universal Scripts | Prospero H.Q. | Weapons Vendor Loader
//      - This script launches the weapons vendor menu when the sphere is entered.
//------------------------------------------------------------------------------------------------------
:Script_Universal_ProsperoHQ_WeaponsVendor_Loader
script_name 'HQWPNVL'
// 0@ = Weapons Vendor
Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|HQWPNVL] [INFO] Script started.")

while true
    wait $mission_trigger_wait_time
    if $playerArea == 3
    then
        func_runExternalScript(externalHQWeaponsVendor, "External-HQWeaponsVendor")
        repeat
            wait $mission_trigger_wait_time
        until not $playerArea == 3
        Break
    else Break
    end    
end
StreamedScript.MarkAsNoLongerNeeded(externalHQWeaponsVendor)
Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|HQWPNVL] [INFO] External script marked no longer needed.")
terminate_this_script
//
//
//
//------------------------------------------------------------------------------------------------------
//      Universal Scripts | Prospero H.Q. | The Custodian (Set Piece) (Loader)
//      - This script spawns The Custodian in his office based on time, with Carter and Davis guarding.
//------------------------------------------------------------------------------------------------------
:Script_Universal_ProsperoHQ_CustodianSetPiece_Loader
script_name 'HQSETCL'
Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|HQSETCL] [INFO] Script started.")

while true
    Clock.GetTimeOfDay(0@, 1@)
    wait $mission_trigger_wait_time
    if and
        $ONMISSION == False
        $playerArea == 3
        0@ >= 6
        not 0@ >= 21
    then
        func_runExternalScript(externalHQSetpieceCustodian, "External-HQSetpieceCustodian")
        repeat
            wait 0
        until not $playerArea == 3
        Break
    end
    
    if not $playerArea == 3
    then Break
    end
end
StreamedScript.MarkAsNoLongerNeeded(externalHQSetpieceCustodian)
terminate_this_script
//
//
//
//------------------------------------------------------------------------------------------------------
//      Universal Scripts | Prospero H.Q. | Interior Agents (Loader)
//      - Creates a random assortmment of agents roaming the H.Q. interior. Loader script.
//------------------------------------------------------------------------------------------------------
:Script_Universal_ProsperoHQ_Agents_Interior_Loader
script_name 'HQAGTIL'
Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|HQAGTIL] [INFO] Script started.")  

while true
    wait $mission_trigger_wait_time
    if $playerArea == 3
    then 
        func_runExternalScript(externalHQAgentsInterior, "External-HQAgentsInterior")
        repeat
            wait 1000
            Char.GetAreaVisible($scplayer, 0@)
        until not $playerArea == 3
        Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|HQAGTIL] [INFO] Script ended. Restarting loader script.")
        StreamedScript.MarkAsNoLongerNeeded(externalHQAgentsInterior)       
    end
end
terminate_this_script
//
//
//
//------------------------------------------------------------------------------------------------------
//      Universal Scripts | Prospero H.Q. | Sarah (Loader)
//      - Creates Sarah in the H.Q. behind the desk.
//------------------------------------------------------------------------------------------------------
:Script_Universal_ProsperoHQ_Sarah_Loader
script_name 'HQSARAL'
Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|HQSARAL] [INFO] Script started.")  

while true
    wait 0
    if and
        $playerArea == 3
        not $flagFollowerActive == followerSarah
    then 
        func_runExternalScript(externalHQSarah, "External-HQSarah")
        repeat
            wait 1000
            Char.GetAreaVisible($scplayer, 0@)
        until not $playerArea == 3
        Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|HQSARAL] [INFO] Script ended. Restarting loader script.")
        StreamedScript.MarkAsNoLongerNeeded(externalHQSarah)       
    end
end
terminate_this_script



//------------------------------------------------------------------------------------------------------
//      Universal Scripts | Anomalies | Mimic
//      - Mimic
//------------------------------------------------------------------------------------------------------
:Script_Universal_Anomaly_Mimic
script_name 'ANOMMMC'
// 0@: Mimic Object
// 1@: Target Char
// 2@: Target Char Model
// 3@: Mimic Equipped
// 4@: Model Active
// 5@: Transform FX Particle
// 6@: Player X
// 7@: Player Y
// 8@: Player Z
3@ = 0

repeat
    wait 0
until Pad.TestCheat("MIMIC")
Streaming.RequestModel(2976)
Streaming.LoadAllModelsNow()
0@ = Object.Create(2976, 0.0, 0.0, 0.0)
Object.SetScale(0@, 0.01)
Task.PickUpObject({Char:} $scplayer, {Object:}0@, {Offset Pos3:}0.0, 0.0, 0.0, {BoneID:}6, {Unknown}16, {Anim Name:}"NULL", {Anim File:}"NULL", {Duration:}-1)

Object.SetCollision(0@, False)
Object.SetModelAlpha(0@, 0) 
Text.PrintStringNow("Mimic obtained!", 3000)
Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|ANOMMMC] [INFO] Obtained")


:Script_Universal_Anomaly_Mimic_Loop
while true
    wait 0
    if Pad.IsKeyPressed(KeyCode.Y)
    then
        if 3@ == 0
        then
            Object.SetModelAlpha(0@, 255) 
            3@ = 1
        else
            Object.SetModelAlpha(0@, 0)
            3@ = 0
        end
        wait 1000
    end    
    
    if and
        Pad.IsKeyPressed(KeyCode.U)
        3@ == 1
    then
        if 4@ == 0
        then
            Player.GetCharIsTargeting($player1, 1@)
            if Char.DoesExist(1@)
            then
                Text.PrintStringNow("Switch Successful", 1000)
                Char.GetModel(1@, 2@)
                Streaming.RequestModel(2@)
                Streaming.LoadAllModelsNow()
                Player.SetModel($player1, 2@)
                Char.MarkAsNoLongerNeeded(1@)
                Streaming.MarkModelAsNoLongerNeeded(2@)
                gosub @Script_Universal_Anomaly_Mimic_Effect
                4@ = 1      
            else
                Text.PrintStringNow("Switch Failed", 1000)
            end
        else
            Text.PrintStringNow("Switched back", 1000)
            Player.SetModel($player1, #NULL)
            gosub @Script_Universal_Anomaly_Mimic_Effect
            4@ = 0
        end
        wait 1000    
    end 
end
terminate_this_script

:Script_Universal_Anomaly_Mimic_Effect
Char.GetCoordinates($scplayer, {Pos3:}6@, 7@, 8@)
5@ = Particle.Create("CAMFLASH", {Pos3:}6@, 7@, 8@, {Ignore Bounding Checks:}True)
Particle.Play(5@)
wait 1000
Particle.Kill(5@)
return
//
//
//
//------------------------------------------------------------------------------------------------------
//      Universal Scripts | Anomalies | Chrono
//      - Chrono
//------------------------------------------------------------------------------------------------------
:Script_Universal_Anomaly_Chrono
script_name 'ANOMCHR'
Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|ANOMCHR] [INFO] Script started.") 
1@ = 0 // Held
2@ = 0 // Active

while true
    wait 0
    if Pad.TestCheat("CHRONO")
    then
        Streaming.RequestModel(2976)
        Streaming.LoadAllModelsNow()
        0@ = Object.Create(2976, 0.0, 0.0, 0.0)
        Object.SetScale(0@, 0.01)
        Task.PickUpObject({Char:} $scplayer, {Object:}0@, {Offset Pos3:}0.0, 0.0, 0.0, {BoneID:}6, {Unknown}16, {Anim Name:}"NULL", {Anim File:}"NULL", {Duration:}-1)
        
        while true
            wait 0
            if and
                Pad.IsKeyPressed(KeyCode.U)
                1@ == 1
            then
                repeat
                    wait 0
                until not Pad.IsKeyPressed(KeyCode.U)
                if 2@ == 0
                then
                    Clock.SetTimeScale(0.5)
                    2@ = 1
                    // slow down
                else
                    Clock.SetTimeScale(1.0)
                   // speed up
                   2@ = 0 
                end
            end
            
            if Pad.IsKeyPressed(KeyCode.Y)
            then
                repeat
                    wait 0
                until not Pad.IsKeyPressed(KeyCode.Y)
                if 1@ == 0
                then
                    Object.SetModelAlpha(0@, 255)
                    1@ = 1    
                else
                    Object.SetModelAlpha(0@, 0)
                    1@ = 0 
                end
            end
        end
    end              
end 



//------------------------------------------------------------------------------------------------------
//      Universal Scripts | Followers | Sarah (Loader)
//      - Loads Sarah's external script.
//------------------------------------------------------------------------------------------------------
:Script_Universal_Follower_Sarah
script_name 'FLWRSWL'
Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|FLWRSWL] [INFO] Script started.") 
while true
    wait 0
    // Check player hasn't cancelled recruitment, and only fire when player leaves the H.Q.
    if not $playerArea == 3
    then
        func_runExternalScript(externalFollowerSarah, "External-FollowerSarah")
        Break
    end
    // 
    if $flagFollowerActive == followerNone
    then 
        Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|FLWRSWL] [INFO] Script ended (Reason: No longer set as active follower via flag).") 
        Break
    end
end
terminate_this_script



//------------------------------------------------------------------------------------------------------
//      Universal Scripts | Facilities | Lil Probe Inn
//      - 
//------------------------------------------------------------------------------------------------------
:Script_Universal_Facility_LilProbeInn
script_name 'XFACLPI'
// 0@: Pickup
// 1@: Teleporter Frame
// 2@: Teleporter Desk 1
// 3@: Teleporter Desk 2

Pickup.CreateForSaleProperty({Pos3:}-86.2929, 1382.7775, 10.2734, 10000, "PROP_3", 0@)
1@ = Blip.AddSpriteForCoord({Pos3:}-86.2929, 1382.7775, 10.2734, RadarSprite.PropertyG)

while true
    wait $mission_trigger_wait_time
    if and
        Player.IsPlaying($player1)
        $ONMISSION == False
        Pickup.HasBeenCollected(0@)
    then        
        Player.SetControl($player1, False)
        Game.SetEveryoneIgnorePlayer($player1, True)
        Hud.SwitchWidescreen(True)
        Camera.SetFixedPosition({Pos3:}-66.8304, 1390.0591, 15.9004, {Rot. Pos3:}0.0, 0.0, 0.0) 
        Camera.PointAtPoint({Pos3:}-67.7256, 1389.6855, 15.6575, SwitchType.Jumpcut) 
        Blip.Remove(1@)
        Text.PrintBig("XFLPI02", 5000, TextStyle.BottomRight)
        Audio.PlayMissionPassedTune(1)
        wait 5000
        Text.ClearPrints()
        Camera.SetBehindPlayer()
        Camera.RestoreJumpcut()
        Hud.SwitchWidescreen(False)
        Player.SetControl($player1, True)
        Game.SetEveryoneIgnorePlayer($player1, False)
        $flagFacilityLilProbeInnPurchased = 1
        terminate_this_script        
    end
end
//
//
//
//------------------------------------------------------------------------------------------------------
//      Universal Scripts | Facilities | Missionary Hill
//      - 
//------------------------------------------------------------------------------------------------------
:Script_Universal_Facility_MissionaryHill
script_name 'XFACMIS'
0@ = Pickup.CreateForSaleProperty({Pos3:}-2516.6189, -622.7719, 132.7585, 10000, "PROP_3")
1@ = Blip.AddSpriteForCoord(-2516.6189, -622.7719, 132.7585, RadarSprite.PropertyG)

while true
    wait $mission_trigger_wait_time
    if and
        Player.IsPlaying($player1)
        $ONMISSION == False
        Pickup.HasBeenCollected(0@)
    then
        Streaming.RequestModel(#A51_JETPSTUFF)
        Streaming.RequestModel(#A51_SDSK_2_)
        Streaming.RequestModel(#A51_SDSK_4_)
        Streaming.LoadAllModelsNow()
        1@ = Object.Create(#A51_JETPSTUFF, {Pos3:}-2528.139, -613.367, 131.560)
        Object.SetHeading(1@, 315.0)
        2@ = Object.Create(#A51_SDSK_2_, {Pos3:}-2527.279, -616.050, 131.568)
        Object.SetHeading(2@, 135.0)
        3@ = Object.Create(#A51_SDSK_2_, {Pos3:}-2535.355, -613.898, 131.560)
        
        Player.SetControl($player1, False)
        Game.SetEveryoneIgnorePlayer($player1, True)
        Hud.SwitchWidescreen(True)
        Camera.SetFixedPosition({Pos3:}-2469.1597, -591.2800, 147.0769, {Rot. Pos3:}0.0, 0.0, 0.0) 
        Camera.PointAtPoint({Pos3:}-2469.9814, -591.7629, 146.7744, SwitchType.Interpolation) 
        Blip.Remove(1@)
        Text.PrintBig("XFLPI02", 5000, TextStyle.BottomRight)
        Audio.PlayMissionPassedTune(1)
        wait 5000
        Camera.SetBehindPlayer()
        Camera.Restore()
        Hud.SwitchWidescreen(False)
        Player.SetControl($player1, True)
        Game.SetEveryoneIgnorePlayer($player1, False)
        $flagFacilityMissionaryHillPurchased = 1
        terminate_this_script        
    end
end



//------------------------------------------------------------------------------------------------------
//      Universal Scripts | TP Network (Loader)
//      - 
//------------------------------------------------------------------------------------------------------
:Script_Universal_TPNetwork_Loader
script_name 'TPNETWL'
Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|TPNETWL] [INFO] Script started.")  
while true
    wait 0
    while true
        wait 0
        if or // Dupe this construct to go beyond 7 conditions.
            $gurgle == 56 // TP locations
            Char.LocateOnFoot3D($scplayer, {Pos3:}2582.03250, 2836.8716, 10.8203, {Rad. Pos3:}1.0, 1.0, 1.0, True) // Prospero H.Q.
            Char.LocateOnFoot2D($scplayer, {Pos3:}-2526.1526, -617.1046, 1.0, 1.0, True) // Missionary Hill
        then Break
        end 
    end
    
    func_runExternalScript(externalTPNetwork, "External-TPNetwork")
    repeat
        wait 0
    until $flag_TPNetwork_Finished == True
end
terminate_this_script



//------------------------------------------------------------------------------------------------------
//      Universal Scripts | Debug | Debug Console
//      - A debug development console containing useful tools and readouts.
//------------------------------------------------------------------------------------------------------

:Debug_DebugConsole
script_name 'DBGCONL' 
Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|DBGCONL] [INFO] Script started.")  

while true
    wait 0
    if Pad.IsKeyPressed(KeyCode.F6)
    then
        repeat 
            wait 0
        until not Pad.IsKeyPressed(KeyCode.F6)
        func_runExternalScript(externalDebugConsole, "External-DebugConsole")
    end
    
    while $flagDebugConsoleOpen == True
        wait 0
    end
    StreamedScript.MarkAsNoLongerNeeded(externalDebugConsole)
end
terminate_this_script



//------------------------------------------------------------------------------------------------------
//      Universal Scripts | Debug | Debug Follower
//      - A quick and dirty follower script to test interactions with other scripts.
//------------------------------------------------------------------------------------------------------
:Debug_Follower
script_name 'DBGFLWR'
Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|DBGFLWR] [INFO] Script started.")  
{   -- Var Mapping --
    0@: Player X
    1@: Player Y
    2@: Player Z
    3@: Proximity Radius
    4@: Blip
}
3@ = 150.0

Streaming.RequestModel(#WFYST)
Streaming.RequestModel(#M4)
Streaming.LoadAllModelsNow()
Char.GetOffsetInWorldCoords($scplayer, 5.0, 0.0, 0.0, 0@, 1@, 2@)
Char $cFollower = Char.Create(PedType.Gang10, #WFYST, 0@, 1@, -100.0)
Char.GiveWeapon($cFollower, 31, 99999)
Char.ShutUp($cFollower, True)
Char.SetSuffersCriticalHits($cFollower, False)
Char.SetProofs($cFollower, True, True, True, True, True)
Group.SetMember($playerGroup, $cFollower)
Char.SetNeverLeavesGroup($cFollower, True)
Char.SetRelationship($cFollower, RelationshipType.Respect, PedType.Player1)
Char.SetRelationship($cFollower, RelationshipType.Like, PedType.Gang9) // Prospero
Char.SetRelationship($cFollower, RelationshipType.Hate, PedType.Gang8) // Myths
$flagFollowerActive = followerSarah
Streaming.MarkModelAsNoLongerNeeded(#WFYST)
Streaming.MarkModelAsNoLongerNeeded(#M4)
Blip.AddForChar($cFollower, 4@)
Blip.SetAsFriendly(4@, True)
Blip.SetAlwaysDisplayOnZoomedRadar(4@, True)
Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|DBGFLWR] [INFO] Spawned debug follower.") 

:Debug_Follower_Loop
wait 0
if or
    Player.IsDead($player1)
    Pad.TestCheat("FOLLOW")
then
    Char.RemoveElegantly($cFollower)
    Char.MarkAsNoLongerNeeded($cFollower)
    Blip.Remove(4@)
    wait 1000
    $flagFollowerActive = False
    Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|DBGFLWR] [INFO] Removed debug follower.") 
    terminate_this_script
end

if not Char.LocateAnyMeansChar3D($cFollower, $scplayer, 3@, 3@, 3@, False)
then
    Char.GetOffsetInWorldCoords($scplayer, 5.0, 0.0, 0.0, 0@, 1@, 2@)
    Char.SetCoordinates($cFollower, 0@, 1@, -100.0)
end
jump @Debug_Follower_Loop
terminate_this_script



//------------------------------------------------------------------------------------------------------
//      Universal Scripts | Debug | Debug Myth
//      - A quick and dirty myth script to test interactions with other scripts.
//------------------------------------------------------------------------------------------------------
:Debug_Myth
script_name 'DBGMYTH'
Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|DBGMYTH] [INFO] Script started.")  
{   -- Var Mapping --
    0@: Player X
    1@: Player Y
    2@: Player Z
    3@: Myth
    4@: Blip
}
Char.GetOffsetInWorldCoords($scplayer, 5.0, 0.0, 0.0, 0@, 1@, 2@)
Streaming.LoadSpecialCharacter(1, "BIGFOOT")
Streaming.LoadAllModelsNow()
wait 0
Char.Create(PedType.Gang8, #SPECIAL01, 0@, 1@, 2@, 3@)
func_applyMythTemplate(3@)
Task.KillCharOnFoot(3@, $scplayer)
Char.SetAnimGroup(3@, "oldman")
Char.SetDecisionMaker(3@, 2)
Char.SetRelationship(3@, RelationShipType.Hate, PedType.Player1)
Char.SetRelationship(3@, RelationShipType.Hate, PedType.Gang9) // Prospero
Char.SetRelationship(3@, RelationShipType.Hate, PedType.Gang10) // Followers
$flagDebugMythActive = True
Streaming.UnloadSpecialCharacter(1)
Blip.AddForChar(3@, 4@)
Blip.SetAsFriendly(4@, False)
Blip.SetAlwaysDisplayOnZoomedRadar(4@, True)
Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|DBGMYTH] [INFO] Spawned debug myth.") 

:Debug_Myth_Loop
wait 0
if or
    Char.IsDead(3@)
    not Char.LocateAnyMeansChar2D($scplayer, 3@, 500.0, 500.0, False)
    Pad.TestCheat("MYTH")
then
    Char.RemoveElegantly(3@)
    Char.MarkAsNoLongerNeeded(3@)
    Blip.Remove(4@)
    wait 1000
    $flagDebugMythActive = False
    Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|DBGMYTH] [INFO] Removed debug myth.") 
    terminate_this_script
else
    jump @Debug_Myth_Loop
end
terminate_this_script



//------------------------------------------------------------------------------------------------------
//      Universal Scripts | Debug | Party Bus
//      - The party bus rolls up on the target.
//------------------------------------------------------------------------------------------------------
:Script_Debug_PartyBus
script_name 'DBGPRTB'
// 0@, 1@, 2@: Player XYZ
// 3@: Bus
// 4@: Driver
// 5@, 6@, 7@, 8@, 9@: Agents
// 10@: Bus Blip
// 11@, 12@, 13@: Target Marker Coords
// 14@: Skip Heading
// 15@: Skipped?

while true
    repeat
        wait 0
    until Pad.TestCheat("BUS")
    Streaming.RequestModel(#COACH)
    Streaming.RequestModel(#BMYMIB)
    Streaming.RequestModel(#WMOMIB)
    Streaming.RequestModel(#DESERT_EAGLE)
    Streaming.LoadAllModelsNow()
    Char.GetCoordinates($scplayer, 0@, 1@, 2@)
    Path.GetClosestCarNode({Pos3:}0@, 1@, 2@, {Pos3:}0@, 1@, 2@)
    
    3@ = Car.Create(#COACH, {Pos3:}0@, 1@, 2@)
    4@ = Char.CreateInsideCar(3@, PedType.Gang9, #WMOMIB)
    5@ = Char.CreateAsPassenger(3@, PedType.Gang9, #BMYMIB, SeatId.FrontRight)
    6@ = Char.CreateAsPassenger(3@, PedType.Gang9, #WMOMIB, SeatId.RearLeft)
    7@ = Char.CreateAsPassenger(3@, PedType.Gang9, #BMYMIB, SeatId.RearRight)
    8@ = Char.CreateAsPassenger(3@, PedType.Gang9, #WMOMIB, SeatId.Passenger4)
    9@ = Char.CreateAsPassenger(3@, PedType.Gang9, #BMYMIB, SeatId.Passenger5)
    10@ = Blip.AddForCar(3@) 
    Char.GiveWeapon(4@, WeaponType.DesertEagle, 99999)
    Char.GiveWeapon(5@, WeaponType.DesertEagle, 99999)
    Char.GiveWeapon(6@, WeaponType.DesertEagle, 99999)
    Char.GiveWeapon(7@, WeaponType.DesertEagle, 99999)
    Char.GiveWeapon(8@, WeaponType.DesertEagle, 99999)
    Char.GiveWeapon(9@, WeaponType.DesertEagle, 99999)
    
    Char.GetCoordinates($scplayer, 0@, 1@, 2@)
    Task.CarDriveToCoord(4@, 3@, {Pos3:}2@, 3@, 4@, 15.0, DriveMode.Accurate, 0, 0)
    
    repeat
        wait 0
        if Char.LocateAnyMeansCar3D($scplayer, 3@, 10.0, 10.0, 10.0, False)
        then Char.ClearTasks(4@)
        end
        
        if and
            Char.LocateAnyMeansCar3D($scplayer, 3@, 10.0, 10.0, 10.0, False)
            Pad.IsKeyPressed(KeyCode.Y)
        then 
            gosub @Script_Debug_PartyBus_StallYReleased
            Break
        end
    until False
    Player.SetControl($player1, False)
    Task.EnterCarAsPassenger($scplayer, 3@, -1, SeatId.Passenger6)
    if $flagFollowerActive == True
    then Task.EnterCarAsPassenger($cFollower, 3@, -1, SeatId.Passenger7)
    end
    repeat
        wait 0
    until Char.IsInCar($scplayer, 3@)   
    Player.SetControl($player1, True)
    
    repeat
        wait 0
        World.GetTargetCoords(11@, 12@, 13@)
        if Pad.IsKeyPressed(KeyCode.Y)
        then Break
        else Text.PrintStringNow("Set a target marker and press ~y~Y~w~.", 1000)
        end        
    until False 
    Task.CarDriveToCoord(4@, 3@, {Pos3:}11@, 12@, 13@, 30.0, DriveMode.Normal, 0, 0)
    gosub @Script_Debug_PartyBus_StallYReleased
    
    repeat
        wait 0
        if Car.Locate2D(3@, 11@, 12@, 10.0, 10.0, False)
        then Break
        end
        
        if and
            Pad.IsKeyPressed(KeyCode.Y)
            15@ == 0
        then
            gosub @Script_Debug_PartyBus_StallYReleased
            15@ = 1
            Camera.DoFade(3000, Fade.Out)
            wait 3500
            Path.GetClosestStraightRoad({From:}11@, 12@, 13@, {Min-Max:}100.0, 200.0, {Node 1 Pos3:}16@, 17@, 18@, {Node 2 Pos3:}0@, 1@, 2@, {Heading:}14@)
            Streaming.RequestCollision(16@, 17@)
            Streaming.LoadAllModelsNow()
            Car.SetCoordinates(3@, 16@, 17@, 18@)
            Car.SetForwardSpeed(3@, 15.0)
            Task.CarDriveToCoord(4@, 3@, {Pos3:}11@, 12@, 13@, 30.0, DriveMode.Normal, 0, 0)
            wait 3000
            Camera.DoFade(3000, Fade.In)              
        end
    until False
    Char.ClearTasks(4@)
    
    repeat
        wait 0
    until not Char.IsInCar($scplayer, 3@)
    Task.LeaveCar(4@, 3@)
    Task.LeaveCar(5@, 3@)
    Task.LeaveCar(6@, 3@)
    Task.LeaveCar(7@, 3@)
    Task.LeaveCar(8@, 3@)
    Task.LeaveCar(9@, 3@)
    if Char.DoesExist($cFollower)
    then Task.LeaveCar($cFollower, 3@)
    end
    Group.SetMember($playerGroup, 4@)
    Group.SetMember($playerGroup, 5@)
    Group.SetMember($playerGroup, 6@)
    Group.SetMember($playerGroup, 7@)
    Group.SetMember($playerGroup, 8@)
    Group.SetMember($playerGroup, 9@)
    Char.SetNeverLeavesGroup(4@, True)
    Char.SetNeverLeavesGroup(5@, True)
    Char.SetNeverLeavesGroup(6@, True)
    Char.SetNeverLeavesGroup(7@, True)
    Char.SetNeverLeavesGroup(8@, True)
    Char.SetNeverLeavesGroup(9@, True)
    Break   
end
terminate_this_script

:Script_Debug_PartyBus_StallYReleased
repeat
    wait 0
until not Pad.IsKeyPressed(KeyCode.Y)
return



//========================================================================================================================================================================================================
//      7. Storymode Scripts
//      Scripts that run exclusively in Storymode.
//========================================================================================================================================================================================================

//------------------------------------------------------------------------------------------------------
//      Storymode Scripts | Mission | Arrival (Loader)
//      - The script launches the Arrival mission.
//------------------------------------------------------------------------------------------------------
:Script_Storymode_Arrival_Loader
script_name 'SMARIVL'
Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|SMARIVL] [INFO] Script started.")

0@ = Blip.AddSpriteForCoord(2485.9832, 2770.8528, 10.7738, RadarSprite.Crash1)
Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|SMARIVL] [INFO] Launching mission 'arrival'...")
Mission.LoadAndLaunchInternal(Mission_Arrival)
repeat
    wait $mission_trigger_wait_time
until $flagMissionArrivalCompleted == True
Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|_______] [INFO] Returned from mission 'arrival'.")  
Blip.Remove(0@)
$ONMISSION = False  
start_new_script @Script_Storymode_FindingTheYeti_Loader
Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|SMARIVL] [INFO] Script ended.")
terminate_this_script



//------------------------------------------------------------------------------------------------------
//      Storymode Scripts | Mission | Finding The Yeti (Loader)
//      - The script launches the Finding The Yeti mission.
//------------------------------------------------------------------------------------------------------
:Script_Storymode_FindingTheYeti_Loader
script_name 'SMFTYEL'
Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|SMFTYEL] [INFO] Script started.")
gosub @Script_Storymode_FindingTheYeti_Loader_Blip

while true
    wait $mission_trigger_wait_time
    if $flag_Storymode_Mission_FindingTheYeti_Completed == True
    then 
        Blip.Remove(0@)
        Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|SMFTYEL] [INFO] Script ended.")
        // start_new_script @Script_Storymode_mis3_Loader
        terminate_this_script
    end
    
    if and 
        $ONMISSION == False
        Player.IsPlaying($player1)
        Player.CanStartMission($player1)
        Char.LocateAnyMeans2D($scplayer, {Pos3:}-2472.0, -1561.0, 412.9375, {Radius:}300.0, {Draw Sphere:}False)
    then
        if and
            $TIME_HOURS >= 3
            22 >= $TIME_HOURS
        then func_doMissionTimeskip({targetHour:}22)
        end
        Text.PrintBig("XSM0301", 1000, TextStyle.BottomRight)
        func_doMissionFade()
        Blip.Remove(0@)
        Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|SMFTYEL] [INFO] Launching mission 'Finding The Yeti'...")
        Mission.LoadAndLaunchInternal(Mission_FindingTheYeti)
    end
    
    if not Player.IsPlaying($player1)
    then
        Blip.Remove(0@)
        gosub @Script_Storymode_FindingTheYeti_Loader_Blip
    end     
end

:Script_Storymode_FindingTheYeti_Loader_Blip
0@ = Blip.AddSpriteForCoord({Pos3:}-2472.0, -1561.0, 412.9375, RadarSprite.Crash1)
return

 

//========================================================================================================================================================================================================
//      8. Freemode Scripts
//      Scripts that run exclusively in Freemode.
//========================================================================================================================================================================================================
:Script_Freemode_Leatherface
script_name 'FMLEATH'
Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|FMLEATH] [INFO] Script started.") 

Clock.GetLocalTime(1@, 1@, 1@, 1@, 1@, 1@, 1@, 0@)
// 1@: Leatherface
// 2@: Blip
// 3@: Has Engaged?

while true
    wait 0
    if Char.LocateAnyMeans3D($scplayer, {Pos3:}-510.0, -128.0, 70.75, 200.0, 200.0, 200.0, False)
    then
        Streaming.LoadSpecialCharacter(1, "LFACE")
        Streaming.RequestModel(#CHNSAW)
        repeat
            wait 0
            if and
                Streaming.HasSpecialCharacterLoaded(1)
                Streaming.HasModelLoaded(#CHNSAW)
            then Break
            end
        until False
        
        Math.GenerateRandomIntInRangeWithSeed(0@, 0, 7, 0@)
        Switch 0@
            Case 0 // SW, Cabin
                1@ = func_freemodeLeatherfaceSpawn({Pos3:}-565.2369, -180.0686, 78.4135, 214.7312)                            
            Case 1 // SW, Cabin
                1@ = func_freemodeLeatherfaceSpawn({Pos3:}-552.5394, -180.362, 78.4062, 237.0016)         
            Case 2 // SE-most open shed
                1@ = func_freemodeLeatherfaceSpawn(-473.3665, -169.6685, 78.2109, 183.131)     
            Case 3 // Centre/NW Cabin
                1@ = func_freemodeLeatherfaceSpawn(-538.5566, -101.2753, 63.3042, 241.3882)
            Case 4 // NW-most open shed
                1@ = func_freemodeLeatherfaceSpawn(-542.8248, -60.4575, 62.9922, 265.515)           
            Case 5 // NE-most Cabin
                1@ = func_freemodeLeatherfaceSpawn(-434.6028, -67.4277, 58.875, 49.6498)          
            Case 6 // Centre, between log piles
                1@ = func_freemodeLeatherfaceSpawn(-478.5486, -123.0586, 65.7223, 71.8966)         
            Case 7 // NW, between log piles
                1@ = func_freemodeLeatherfaceSpawn(-567.1461, -102.5068, 65.1088, 251.3683)
        end
        if $flagDebugMythMarkers == True
        then 2@ = Blip.AddForChar(1@)
        end 
        
        // Main loop
        while true
            wait 0
            // If Leatherface can see the player, then attack
            if Char.HasSpottedCharInFront(1@, $scplayer)
            then Task.KillCharOnFoot(1@, $scplayer)
            end
            
            // If the player is not within 500m of Leatherface,
            if not Char.LocateAnyMeansChar3D($scplayer, 1@, 500.0, 500.0, 500.0, False)
            then // then check if Leatherface has been attacked. 
                if 3@ == True // If he has, freeze his position until the player returns.
                then
                    Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|FMLEATH] [INFO] Player left range, and has engaged. Freezing position until return.") 
                    Char.FreezePosition(1@, True)
                    if Blip.DoesExist(2@)
                    then Blip.Remove(2@)
                    end
                    repeat
                        wait 1000
                    until Char.LocateAnyMeansChar3D($scplayer, 1@, 300.0, 300.0, 300.0, False)
                    if $flagDebugMythMarkers == True
                    then 2@ = Blip.AddForChar(1@)
                    end
                else // Else, cleanup and start the script again
                    Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|FMLEATH] [INFO] Player left range, but did not engage. Cleaning up and restarting.") 
                    gosub @Script_Freemode_Leatherface_Cleanup
                    Break
                end
            end 
            /*
            if Char.IsPlayingAnim(1@, "run_old")
            then Char.SetAnimSpeed(1@, "run_old", 8.0)
            end
            if Char.IsPlayingAnim(1@, "Csaw_1")
            then Char.SetAnimSpeed(1@, "Csaw_1", 8.0)
            end
            if Char.IsPlayingAnim(1@, "Csaw_2")
            then Char.SetAnimSpeed(1@, "Csaw_2", 8.0)
            end
            if Char.IsPlayingAnim(1@, "Csaw_3")
            then Char.SetAnimSpeed(1@, "Csaw_3", 8.0)
            end
            if Char.IsPlayingAnim(1@, "Csaw_g")
            then Char.SetAnimSpeed(1@, "Csaw_g", 8.0)
            end
            if Char.IsPlayingAnim(1@, "Csaw_part")
            then Char.SetAnimSpeed(1@, "Csaw_part", 8.0)
            end
            if Char.IsPlayingAnim(1@, "Weapon_csaw")
            then Char.SetAnimSpeed(1@, "Weapon_csaw", 8.0)
            end
            if Char.IsPlayingAnim(1@, "Weapon_csawlo")
            then Char.SetAnimSpeed(1@, "Weapon_csawlo", 8.0)
            end
            if Char.IsPlayingAnim(1@, "idle_csaw")
            then Char.SetAnimSpeed(1@, "idle_csaw", 8.0)
            end 
            */          
        end
        Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Main|FMLEATH] [INFO] Breaking main loop.") 
        Break
    end    
end
terminate_this_script

function func_freemodeLeatherfaceSpawn(posX: Float, posY: Float, posZ: Float, heading: Float): Int
    Char.Create(PedType.Special, #SPECIAL01, posX, posY, posZ, 10@)
    Char.SetHeading(10@, heading)
    Char.GiveWeapon(10@, WeaponType.Chainsaw, 1)
    func_applyMythTemplate(10@) 
    Char.SetAnimGroup(10@, "OldMan")
    return 10@     
end

:Script_Freemode_Leatherface_Cleanup
Streaming.MarkModelAsNoLongerNeeded(#CHNSAW)
Streaming.UnloadSpecialCharacter(1)
if Char.DoesExist(5@)
then
    Char.Delete(5@)
    Char.MarkAsNoLongerNeeded(5@) 
end
if Blip.DoesExist(6@)
then Blip.Remove(6@)
end
wait 1000
return


//========================================================================================================================================================================================================
//      9. Functions
//      Custom functions called extensively to reduce code bloat.
//========================================================================================================================================================================================================

//------------------------------------------------------------------------------------------------------
//      Functions | Run External Script
//      - This function streams an external script, waits for it to be loaded, then starts it safely.
//------------------------------------------------------------------------------------------------------
function func_runExternalScript(targetScript: int, name: string)
    int scriptCount = StreamedScript.GetNumberOfInstances(targetScript)
    if scriptCount == 0
    then
        StreamedScript.Stream(targetScript)
        while not StreamedScript.HasLoaded(targetScript)
            wait 0
        end
        StreamedScript.StartNew(targetScript)
        Debugger.LogLine({Filename:}logPath, {Timestamp:}True, {Text:}"[Main|_______] [INFO] Began external script '%s'.", name)
    else Debugger.LogLine({Filename:}logPath, {Timestamp:}True, {Text:}"[Main|_______] [ERROR] Failed to start external script '%s'. Active instances already %i.", name, scriptCount)
    end
end



//------------------------------------------------------------------------------------------------------
//      Functions | Teleport Player Safely
//      - This function teleports the player safely to the target coordinates, requesting collision beforehand.
//------------------------------------------------------------------------------------------------------
function func_teleportSafe(posX: float, posY: float, heading: float, area: int)
    Streaming.SetAreaVisible(area)
    Char.SetAreaVisible($scplayer, area)
    Char.SetCoordinates($scplayer, posX, posY, 0.0)
    Streaming.RequestCollision(posX, posY)
    Streaming.LoadAllModelsNow()
    wait 0
    
    // put on loaded ground
    Char.SetCoordinates($scplayer, posX, posY, -100.0)
    Char.SetHeading($scplayer, heading)
    
    float posZ
    //Player is already at the waypoint
    Char.GetCoordinates($scplayer, posX, posY, posZ)
    //We're loading the scene
    Streaming.LoadScene(posX, posY, posZ)
    Streaming.LoadAllModelsNow()
end



//------------------------------------------------------------------------------------------------------
//      Functions | Equip Agent Outfit
//      - This function puts CJ in his agent outfit.
//------------------------------------------------------------------------------------------------------
function func_giveAgentOutfit()
    Player.GiveClothesOutsideShop({Player:} $player1, {Texture:}"Player_Face",   {Model:}"HEAD",      {Body Part:}BodyPart.Head)
    Player.GiveClothesOutsideShop({Player:} $player1, {Texture:}"Tuxedo",        {Model:}"Suit2",     {Body Part:}BodyPart.Torso)
    Player.GiveClothesOutsideShop({Player:} $player1, {Texture:}"Suit1TrBlk2",   {Model:}"Suit1Tr",   {Body Part:}BodyPart.Legs)
    Player.GiveClothesOutsideShop({Player:} $player1, {Texture:}"ShoeDressBlk",  {Model:}"Shoe",      {Body Part:}BodyPart.Shoes)
    Player.GiveClothesOutsideShop({Player:} $player1, {Texture:}"WatchCro2",     {Model:}"Watch",     {Body Part:}14)
    Player.GiveClothesOutsideShop({Player:} $player1, {Texture:}"Glasses05Dark", {Model:}"Glasses03", {Body Part:}BodyPart.Glasses)
    Player.BuildModel($player1)
end



//------------------------------------------------------------------------------------------------------
//      Functions | Equip Casual Outfit
//      - This function puts CJ in his normal casual outfit.
//------------------------------------------------------------------------------------------------------
function func_giveCasualOutfit()
    Player.GiveClothesOutsideShop({Player:} $player1, {Texture:}"Player_Face",    {Model:}"HEAD",   {Body Part:}BodyPart.Head)
    Player.GiveClothesOutsideShop({Player:} $player1, {Texture:}"Tshirtmaddgrey", {Model:}"Tshirt", {Body Part:}BodyPart.Torso)
    Player.GiveClothesOutsideShop({Player:} $player1, {Texture:}"Worktrgrey",     {Model:}"Worktr", {Body Part:}BodyPart.Legs)
    Player.GiveClothesOutsideShop({Player:} $player1, {Texture:}"ShoeDressBlk",   {Model:}"Shoe",   {Body Part:}BodyPart.Shoes)
    Player.GiveClothes           ({Player:} $player1, {Texture:}0,                {Model:}0,        {Body Part:}14)
    Player.GiveClothes           ({Player:} $player1, {Texture:}0,                {Model:}0,        {Body Part:}BodyPart.Glasses)
    Player.BuildModel($player1)
end



//------------------------------------------------------------------------------------------------------
//      Functions | Give Weapon Safely
//      - This function loads the model before giving the weapon to the specified Char.
//------------------------------------------------------------------------------------------------------
function func_giveWeaponSafely(targetChar: Char, weaponID: int, ammo: int)
    Int modelID = Weapon.GetModel(weaponID)
    Streaming.RequestModel(modelID)
    Streaming.LoadAllModelsNow()
    Char.GiveWeapon(targetChar, weaponID, ammo)
    Char.SetCurrentWeapon(targetChar, weaponID)
    Streaming.MarkModelAsNoLongerNeeded(modelID)
end



//------------------------------------------------------------------------------------------------------
//      Functions | Play SCM Sound Safely
//      - This function safely loads and then plays an SCM sound.
//------------------------------------------------------------------------------------------------------
function func_playSoundSafely(slotID: int, soundID: int, waitUntilFinished: int)
    Audio.LoadMissionAudio(slotID, soundID)
    repeat
        wait 0
    until Audio.HasMissionAudioLoaded(slotID)
    Audio.PlayMissionAudio(slotID)
    
    if waitUntilFinished == False
    then
        return
    else 
        repeat
            wait 0
        until Audio.HasMissionAudioFinished(slotID)
    end
end



//------------------------------------------------------------------------------------------------------
//      Functions | Load AudioStream safely.
//      - This function requests an AudioStream and checks it's loaded before playing.
//------------------------------------------------------------------------------------------------------
function func_playAudioStreamSafely(streamID: AudioStream, soundPath: string, waitUntilFinished: int)
    repeat
        wait 0
    until streamID = AudioStream.Load(soundPath)
    AudioStream.SetState(streamID, AudioStreamAction.Play)
    
    if waitUntilFinished == False
    then
        return
    else
        repeat
            wait 0
        until not AudioStream.IsPlaying(streamID)
    end
end



//------------------------------------------------------------------------------------------------------
//      Functions | Play Dialogue
//      - 
//------------------------------------------------------------------------------------------------------
function func_playDialogue(mode: int, asPath: string, scmSoundID: int, subID: string, doAnims: int, forChar: Char, waitUntilFinished: int, extraWaitTime: int)
int streamLength = 0
int subtitleDisplayTime = 0
AudioStream streamID

    if mode == 0 // AudioStream mode.
    then
        repeat
            wait 0
        until AudioStream.Load(asPath, streamID)
        AudioStream.GetLength(streamID, streamLength)
        streamLength *= 1000
        streamLength += 1000
        AudioStream.SetState(streamID, AudioStreamAction.Play)
        if Game.AreSubtitlesSwitchedOn()
        then
            Text.PrintNow(subID, streamLength, 1)
        end  
        
        if doAnims == True
        then
            Task.PlayAnim({Char:}forChar, {Anim Name:}"idle_chat", {Anim File:}"ped", {Speed:}4.0, {Loop:}False, {LockX:}False, {LockY:}False, {Freeze Frame:}False, {Duration:}streamLength)
            Char.StartFacialTalk(forChar, 10)
        end
        
        if waitUntilFinished == True
        then
            repeat
                wait 0
            until not AudioStream.IsPlaying(streamID)
            if Char.DoesExist(forChar)
            then Char.StopFacialTalk(forChar)
            end
            extraWaitTime += 500
            wait extraWaitTime
        end
        AudioStream.Remove(streamID) 
    end
end



//------------------------------------------------------------------------------------------------------
//      Functions | Clear Camera Vector
//      - This function clears stubborn camera vector movements.
//------------------------------------------------------------------------------------------------------
function func_clearCameraVector()
    float cameraX, cameraY, cameraZ
    Camera.GetActiveCoordinates(cameraX, cameraY, cameraZ)
    Camera.SetVectorMove(cameraX, cameraY, cameraZ, cameraX, cameraY, cameraZ, 0, False)
    Camera.SetVectorTrack(cameraX, cameraY, cameraZ, cameraX, cameraY, cameraZ, 0, False)
end



//------------------------------------------------------------------------------------------------------
//      Functions | Restore Camera
//      - This function resets the camera from cutscene mode back to normal mode
//------------------------------------------------------------------------------------------------------
function func_restoreCamera()
    Camera.ResetNewScriptables()
    Camera.RestoreJumpcut()
    Camera.DoFade(0, Fade.In)
    Hud.SwitchWidescreen(False)
    Text.ClearThisPrintBigNow(TextStyle.BottomRight)
end



//------------------------------------------------------------------------------------------------------
//      Functions | Mission Fade
//      - This function fades the game ready to launch a mission
//------------------------------------------------------------------------------------------------------
function func_doMissionFade()
    if Player.IsPlaying($player1)
    then
        $ONMISSION = True
        Player.SetControl($player1, False)      
        Camera.SetFadingColor(0, 0, 0)
        Camera.DoFade(1000, Fade.Out)
        Camera.SetBehindPlayer()
        while Camera.GetFadingStatus()
            wait 0
        end
        Text.ClearPrints()
        Text.ClearHelp()
        Char.ClearTasks($scplayer)
    end
end



//------------------------------------------------------------------------------------------------------
//      Functions | Mission Timeskip
//      - This function accelerates the time of day like when starting missions in GTA V
//------------------------------------------------------------------------------------------------------
function func_doMissionTimeskip(targetHour: Int)
    Int iCurrentHour
    Int iCurrentMinute
    Clock.GetTimeOfDay(iCurrentHour, iCurrentMinute)    
    repeat
        if iCurrentHour == targetHour
        then
            Debugger.LogLine({Filename:}logPath, {Timestamp:}True, {Text:}"[Main|_______] [INFO] Clock now at %i (iCurrentHour). Input was %i (targetHour). Breaking.", iCurrentHour, targetHour,)
            Break
        end        
        iCurrentHour += 1
        Clock.SetTimeOfDay(iCurrentHour, iCurrentMinute)
        Debugger.LogLine({Filename:}logPath, {Timestamp:}True, {Text:}"[Main|_______] [INFO] Clock set to %i. Looping.", iCurrentHour)
        wait 250
    until False
Debugger.LogLine({Filename:}logPath, {Timestamp:}True, {Text:}"[Main|_______] [INFO] Time skip complete at %i. Returning.", iCurrentHour)
end



//------------------------------------------------------------------------------------------------------
//      Functions | Basic Myth Setup
//      - This function applies 'core' properties to myths. Override and extend as required.
//------------------------------------------------------------------------------------------------------
function func_applyMythTemplate(mythChar: Char)
    mythChar.SetDropsWeaponsWhenDead(False)
    mythChar.SetSuffersCriticalHits(False)
    mythChar.SetHealth(20000)
    mythChar.SetMoney(0)
    mythChar.SetUsesUpperbodyDamageAnimsOnly(True)
    mythChar.ShutUp(True)
    mythChar.SetDrownsInWater(False)
    mythChar.SetDecisionMaker(2)
    mythChar.SetRelationship(4, 0)  // Myth hates player
    mythChar.SetRelationship(4, 16) // Myth hates GANG10 (Followers, Prospero etc)
end



//------------------------------------------------------------------------------------------------------
//      Functions | Spawn In Car
//      - This function spawns a car and puts the player in the driver seat. Output handle is returned.
//------------------------------------------------------------------------------------------------------
function func_spawnInCar(targetChar: Char, carModel: Int): int
    float posX, posY, posZ   
    
    Streaming.RequestModel(carModel)
    Streaming.LoadAllModelsNow()
    Char.GetCoordinates(targetChar, {Pos3:}posX, posY, posZ)
    Path.GetClosestCarNode({Input Pos3:}posX, posY, posZ, {Output Pos3:}posX, posY, posZ)
    
    Car c = Car.Create(carModel, {Pos3:}posX, posY, posZ)
    Char.WarpIntoCar(targetChar, c)
    return c
end



//------------------------------------------------------------------------------------------------------
//      Functions | Menu Stall
//      - This function stalls a menu until the input key is released.
//------------------------------------------------------------------------------------------------------
function func_stallMenu(mode: Int)
    Switch mode
        Case 0 // Stall confirm (Sprint)
            while Pad.IsButtonPressed({Pad:}PadID.Pad1, {Button:}Button.Cross) // Sprint
                wait 0
            end
        
        Case 1 // Stall backing out (Return/Backspace)
            repeat
                wait 0
                if and
                    not Pad.IsKeyPressed(KeyCode.Return)
                    not Pad.IsKeyPressed(KeyCode.Back)
                then
                    Break
                end
            until False  
    end      
end



//========================================================================================================================================================================================================
//      10. Missions
//      All missions go here. They are only loaded into memory when called.
//========================================================================================================================================================================================================

//------------------------------------------------------------------------------------------------------
//      Missions | Mission 0: Initial
//      - Initialisation 'mission' which has the main menu etc.
//------------------------------------------------------------------------------------------------------
:Mission_Initial
{$INCLUDE_ONCE main\missions\Mission-Initial.txt}

//------------------------------------------------------------------------------------------------------
//      Missions | Mission 1: Introduction
//      - Intro cinematic where the Bloodpool opens and CJ is at the bar.
//------------------------------------------------------------------------------------------------------
:Mission_Introduction
{$INCLUDE_ONCE main\missions\Mission-Introduction.txt}

//------------------------------------------------------------------------------------------------------
//      Missions | Mission 2: Arrival
//      - CJ goes to Prospero H.Q. and is given his first task.
//------------------------------------------------------------------------------------------------------
:Mission_Arrival
{$INCLUDE_ONCE main\missions\Mission-Arrival.txt}

//------------------------------------------------------------------------------------------------------
//      Missions | Mission 3: Finding The Yeti
//      - CJ heads to Mount Chiliad in search of the Yeti and the missing hikers.
//------------------------------------------------------------------------------------------------------
:Mission_FindingTheYeti
{$INCLUDE_ONCE main\missions\Mission-FindingTheYeti.txt}





//========================================================================================================================================================================================================
//      11. External Scripts
//      All external scripts go here. They are only loaded into memory when called.
//========================================================================================================================================================================================================

//------------------------------------------------------------------------------------------------------
//      External Scripts | 0 | Debug Console
//      - A debug developer console containing useful tools and readouts.
//------------------------------------------------------------------------------------------------------
:External_DebugConsole
{$INCLUDE_ONCE main\externals\External-DebugConsole.txt}



//------------------------------------------------------------------------------------------------------
//      External Scripts | 1 | Agents: Exterior
//      - Spawns a random assortment of agents around Prospero H.Q.'s exterior.
//------------------------------------------------------------------------------------------------------
:External_HQAgentsExterior
{$INCLUDE_ONCE main\externals\External-HQAgentsExterior.txt}



//------------------------------------------------------------------------------------------------------
//      External Scripts | 2 | Agents: Interior
//      - Spawns a random assortment of agents inside Prospero H.Q.'s exterior.
//------------------------------------------------------------------------------------------------------
:External_HQAgentsInterior
{$INCLUDE_ONCE main\externals\External-HQAgentsInterior.txt}


//------------------------------------------------------------------------------------------------------
//      External Scripts | 3 | HQ Weapons Vendor
//      - Creates the weapons purchase menu.
//------------------------------------------------------------------------------------------------------
:External_HQWeaponsVendor
{$INCLUDE_ONCE main\externals\External-HQWeaponsVendor.txt}



//------------------------------------------------------------------------------------------------------
//      External Scripts | 4 | HQ Setpiece Custodian
//      - Makes The Custodian, Carter, and Davis appear at his office between the hours of 6am - 9pm.
//------------------------------------------------------------------------------------------------------
:External_HQSetpieceCustodian
{$INCLUDE_ONCE main\externals\External-HQSetpieceCustodian.txt}



//------------------------------------------------------------------------------------------------------
//      External Scripts | 5 | HQ Sarah
//      - 
//------------------------------------------------------------------------------------------------------
:External_HQSarah
{$INCLUDE_ONCE main\externals\External-HQSarah.txt}



//------------------------------------------------------------------------------------------------------
//      External Scripts | 6 | Follower Sarah
//      - 
//------------------------------------------------------------------------------------------------------
:External_FollowerSarah
{$INCLUDE_ONCE main\externals\External-FollowerSarah.txt}



//------------------------------------------------------------------------------------------------------
//      External Scripts | 7 | TP Network
//      - 
//------------------------------------------------------------------------------------------------------
:External_TPNetwork
{$INCLUDE_ONCE main\externals\External-TPNetwork.txt}
