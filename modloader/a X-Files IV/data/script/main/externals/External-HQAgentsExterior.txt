//========================================================================================================================================================================================================
//------------------------------------------------------------------------------------------- GTA X-Files IV (2024) --------------------------------------------------------------------------------------
//---------------------------------------------------------------------------------------------- By james227uk -------------------------------------------------------------------------------------------
//========================================================================================================================================================================================================
// Name:        Exterior Agents
// Description: This script generates random agents within the Prospero H.Q. exterior grounds.
// Path:        main\externals\External-HQAgentsExterior.txt
// Trigger:     Upon getting close to Prospero H.Q.
//========================================================================================================================================================================================================
{$USE CLEO}
{$USE CLEO+}
{$USE NewOpcodes}
script_name 'HQAGTES'
NOP
var cArrayAgents: array 25 of Char 
Int iArrayAgentsIndex = 0
Int iRandomModelFlip = 165
Float fRandomX, fRandomY, fRandomZ
Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Exts|HQAGTES] [INFO] Script started")

Streaming.RequestModel(#BMYMIB)
Streaming.RequestModel(#WMOMIB)
repeat
    wait 0
    if and
        Streaming.HasModelLoaded(#BMYMIB)
        Streaming.HasModelLoaded(#WMOMIB)
    then
        Break
    end
until False 

repeat
    gosub @GenerateRandomCoords
    Char.Create(PedType.Gang9, iRandomModelFlip, fRandomX, fRandomY, 9.820312, cArrayAgents[iArrayAgentsIndex])
    Task.WanderStandard(cArrayAgents[iArrayAgentsIndex])
    if iRandomModelFlip == 165
    then iRandomModelFlip = 166
    else iRandomModelFlip = 165
    end
    iArrayAgentsIndex += 1
until iArrayAgentsIndex == 25
PLAY_MISSION_PASSED_TUNE 2
iArrayAgentsIndex = 0

repeat
    wait 1000
until not Char.LocateAnyMeans3D($scplayer, {Pos3:}2497.2947, 2773.2153, 10.8262, 300.0, 300.0, 300.0, False)

repeat
    Char.ClearTasksImmediately(cArrayAgents[iArrayAgentsIndex])
    Char.MarkAsNoLongerNeeded(cArrayAgents[iArrayAgentsIndex])
    iArrayAgentsIndex += 1
until iArrayAgentsIndex == 25
Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Exts|HQAGTES] [INFO] Distance exceeded. Script ending.")
terminate_this_script


:GenerateRandomCoords
fRandomX = Math.RandomFloatInRange(2500.0, 2748.22)
fRandomY = Math.RandomFloatInRange(2705.1375, 2848.2803)  
fRandomZ = Math.RandomFloatInRange(10.8203, 21.2580)
fRandomZ = World.GetGroundZFor3DCoord(fRandomX, fRandomY, fRandomZ)
if fRandomZ < 12.0
then return
else jump @GenerateRandomCoords
end 
return
