//==========================================================================================================================================================================================================
//------------------------------------------------------------------------------------------- GTA X-Files IV (2024) ----------------------------------------------------------------------------------------
//---------------------------------------------------------------------------------------------- By james227uk ---------------------------------------------------------------------------------------------
//==========================================================================================================================================================================================================
// Name:        1. Introduction
// Description: Intro cinematic. Hikers walk by the Bloodpool, and CJ is called to H.Q.
// Path:        main\missions\Mission-Introduction.txt
// Trigger:     Upon Storymode selected.
// Notes:       
//==========================================================================================================================================================================================================
:Mission_Introduction_Header
script_name 'introd'
Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Mssn|INTROD] [INFO] Loaded mission 'introduction'.")


//------------------------------------------------------------------------------------------------------
//      Main Mission Body
//------------------------------------------------------------------------------------------------------
//--- Part 1: Title card
//    A simple title card displaying X-Files IV.
//------------------------------------------------------------------------------------------------------
:Mission_Introduction_Start
Weather.ForceNow(WeatherType.ExtraSunnySf)
Clock.SetTimeOfDay(12, 0)
func_giveCasualOutfit()

skip_cutscene_start_internal @Mission_Introduction_Scene2
    Char.SetCoordinates($scplayer, {Pos3:}-1351.6396, 900.3057, 46.8549)
    Camera.SetFixedPosition({Pos3:}-1290.726562, 799.381958, 1.0, {Offset Pos3:}0.0, 0.0, 0.0)
    Camera.PointAtPoint({Pos3:}-90361.820312, 702.167358, 44183.246094, SwitchType.JumpCut)
    Camera.SetVectorTrack({From Pos3:}-50716.812500, 83541.726562, 17745.085938, {To Pos3:}-90361.820312, 702.167358, 44183.246094, {Time:}9000, {Ease:}False)
    Streaming.RequestCollision({Pos2:}-1351.6396, 900.3057)
    Streaming.LoadAllModelsNow()  
    Camera.DoFade(1000, Fade.In)
    wait 1000
    Text.PrintBig("XSM0101", 10000, TextStyle.Middle) // X-Files IV
    wait 3000
    Text.PrintNow("XSM0102", 7000, 2) // By james227uk
    wait 5000
    Camera.PointAtPoint({Pos3:}-90361.820312, 702.167358, 44183.246094, SwitchType.JumpCut)
    wait 2000
    Camera.DoFade({Time:}3000, {Direction:}Fade.Out)
    wait 5000

//--- Part 2: A walk in the woods
//    Several hikers walk through the woods, and are then attacked when the Bloodpool opens.
//------------------------------------------------------------------------------------------------------
:Mission_Introduction_Scene2    
skip_cutscene_end
skip_cutscene_start_internal @Mission_Introduction_Scene3

// Load collision. Load models.
Char.SetCoordinates($scplayer, {Pos3:}-636.7043, -1926.9922, 19.1481)
Streaming.RequestCollision({Pos2:}-636.7043, -1926.9922)
Streaming.RequestModel(#DWMYLC1) // Cowboy hat male
Streaming.RequestModel(#CWFYHB)  // Country Female
Streaming.RequestModel(#CWMYFR)  // Country Male
Streaming.RequestModel(#DNFYLC)  // Country female - Eve's Mom
Streaming.RequestModel(#SADLER)  // Family car
Streaming.RequestAnimation("ON_LOOKERS")
Streaming.LoadAllModelsNow()
Audio.LoadMissionAudio(MissionAudioSlot.Slot1, 15018) // Get a move on there.
Audio.LoadMissionAudio(MissionAudioSlot.Slot2, 66032) // Hey! Don't be so rude!  | SPC_GA, Bank 39, Sound 033
wait 0
Char.SetCoordinates($scplayer, -636.7043, -1926.9922, -100.0)

// Create scene
Char cIntroMale1 = Char.Create(PedType.Special, #DWMYLC1, {Pos3:}-695.3566, -1853.7377, -100.0)
cIntroMale1.SetHeading(96.1941)
Char cIntroFemale1 = Char.Create(PedType.Special, #CWFYHB, {Pos3:}-693.2648, -1853.0255, -100.0)
cIntroFemale1.SetHeading(83.3473)
Char cIntroMale2 = Char.Create(PedType.Special, #CWMYFR, {Pos3:}-693.4182, -1854.9003, -100.0)
cIntroMale2.SetHeading(97.7608)
Char cIntroFemale2 = Char.Create(PedType.Special, #DNFYLC, {Pos3:}-689.09, -1851.6373, -100.0)
cIntroMale1.SetHeading(101.2076)
Car vIntroCar = Car.Create(#SADLER, {Pos3:}-798.6021, -1875.5094, 11.6531)
vIntroCar.SetHeading(125.3343)

// Camera tracks down the Bloodpool stream.
Camera.SetVectorMove({From Pos3:}-496.417419, -1891.682373, 15.819115, {To Pos3:}-620.647827, -1892.692871, 16.613722, {Time:}10000, {Ease:}False)
Camera.SetVectorTrack({From Pos3:}-1495.541504, -1883.166504, 40.493820, {To Pos3:}-1559.366089, -1945.964966, -1.184813, {Time:}10000, {Ease:}False)
Camera.DoFade(3000, Fade.In)
wait 8000
Task.CharSlideToCoord(cIntroMale1,   {Pos3:}-719.0985, -1853.6321, 14.2191,  {Stop Heading:}93.5026, {Stop Radius:}0.1)
Task.CharSlideToCoord(cIntroFemale1, {Pos3:}-717.6971, -1853.2827, 14.2098,  {Stop Heading:}97.9547, {Stop Radius:}0.1)
Task.CharSlideToCoord(cIntroMale2,   {Pos3:}-717.6265, -1854.2914, 14.2069,  {Stop Heading:}89.1087, {Stop Radius:}0.1)
Task.CharSlideToCoord(cIntroFemale2, {Pos3:}-715.8428, -1851.1246, 14.1354,  {Stop Heading:}94.1221, {Stop Radius:}0.1)
wait 2000

// Camera watches hikes pass by.
Camera.ResetNewScriptables()
Camera.SetFixedPosition({Pos3:}-702.045349, -1857.600098, 13.582462, {Offset Pos3:}0.0, 0.0, 0.0)
Camera.PointAtPoint({Pos3:}519.739014, -1251.781250, 250.245667, SwitchType.JumpCut)

wait 1000
Audio.PlayMissionAudio(MissionAudioSlot.Slot1) // Male 1: Get a move on there!
repeat
    wait 250
until Audio.HasMissionAudioFinished(MissionAudioSlot.Slot1)
Audio.PlayMissionAudio(MissionAudioSlot.Slot2) // Female 2: Hey, don't be so rude!
repeat
    wait 0
until Audio.HasMissionAudioFinished(MissionAudioSlot.Slot2)
wait 2000
Audio.ClearMissionAudio(MissionAudioSlot.Slot1)
Audio.ClearMissionAudio(MissionAudioSlot.Slot2)
Audio.LoadMissionAudio(MissionAudioSlot.Slot1, 67617) // This weather is bullshit!  | SPC_GA, Bank 47, 018
Audio.LoadMissionAudio(MissionAudioSlot.Slot2, 7835) // What in tarnation?!

// Camera pans up, and a storm suddenly starts.
Camera.SetFixedPosition({Pos3:} -791.940979, -1870.594116, 12.865604, {Offset Pos3:}0.0, 0.0,0.0)
Camera.SetVectorTrack({From Pos3:}-751.5968, -1929.5527, 6.9112, {To Pos3:}-701.2754, -2036.588, 110.6604, {Time:}10000, {Ease:}False)
wait 5000
Weather.ForceNow(WeatherType.RainySf)
cIntroMale1.SetCoordinates(-719.0985, -1853.6321, 14.2191)
cIntroFemale1.SetCoordinates(-717.6971, -1853.2827, 14.2098)
cIntroMale2.SetCoordinates(-717.6265, -1854.2914, 14.2069)
cIntroFemale2.SetCoordinates(-715.8428, -1851.1246, 14.1354)
cIntroMale1.SetHeading(93.5026)
cIntroFemale1.SetHeading(97.9547)
cIntroMale2.SetHeading(89.1087)
cIntroFemale2.SetHeading(94.1221)
wait 5000
Camera.ResetNewScriptables()

// Hikers moan about the weather
Camera.SetFixedPosition({Pos3:}-714.767578, -1856.230347, 14.612205, {Offset Pos3:}0.0, 0.0, 0.0)
Camera.PointAtPoint({Pos3:}-1826.590576, -146.726013, -363.410858, SwitchType.JumpCut)
Task.TurnCharToFaceChar({Target:}cIntroFemale2, {Turn To:}cIntroMale1)
wait 500
Task.PlayAnim({Char:}cIntroFemale2, {Anim:}"IDLE_CHAT", {Anim File:}"PED", {Speed:}4.0, {Loop:}True, {Lock X:}True, {Lock Y:}True, {Freeze Last Frame:}False, {Duration:}-1)
cIntroFemale2.StartFacialTalk({Duration:}-1)
Audio.PlayMissionAudio(MissionAudioSlot.Slot1) // Female 2: This weather is bullshit!
Task.TurnCharToFaceChar({Target:}cIntroMale1, {Turn To:}cIntroFemale2)
Task.TurnCharToFaceChar({Target:}cIntroFemale1, {Turn To:}cIntroFemale1)
repeat
    wait 0
until Audio.HasMissionAudioFinished(MissionAudioSlot.Slot1)
cIntroFemale2.StopFacialTalk()
cIntroFemale2.ClearTasksImmediately()
wait 2000

// Bloodpool opens
Particle pBloodpoolSplash = Particle.Create("CARWASHSPRAY", {Pos3:}-725.0519,  -1882.1888, 6.0388, {Ignore Bounding Checks:}True)
pBloodpoolSplash.Play()

if not Streaming.HasAnimationLoaded("ON_LOOKERS")
then
    Debugger.LogLine({File:}logPath, {Timestamp:}True, {Text:}"[Mssn|INTROD] [WARN] Animation 'ON_LOOKERS' did not load when requested. Forcing load.")
    repeat
        wait 0
        Streaming.RequestAnimation("ON_LOOKERS")
    until Streaming.HasAnimationLoaded("ON_LOOKERS")
end

// Camera in front of male2, who turns to see the Bloodpool open
Camera.SetFixedPosition({Pos3:}-718.554016, -1857.553467, 14.159953, {Offset Pos3:}0.0, 0.0, 0.0)
Camera.PointAtPoint({Pos3:}-258.711670, 138.304016, 164.619583, SwitchType.JumpCut)
AudioStream asRoar = AudioStream.Load("root:\modloader\a X-Files IV\audio\fx\zapslat_roar.mp3")
repeat
    wait 0
until AudioStream.Load("root:\modloader\a X-Files IV\audio\fx\zapslat_roar.mp3", asRoar)

wait 1000
Task.TurnCharToFaceCoord({Char:}cIntroMale2, {Pos3:}-725.0519,  -1882.1888, 6.0388)
wait 1000
Sequence seqAnimPoint = Sequence.Open()
    Task.PlayAnim({Char:}-1, {Anim Name:}"PANIC_IN", {Anim File:}"ON_LOOKERS", {Speed:}4.0, {Loop:}False, {Lock X:}False, {Lock Y:}False, {Freeze Frame:}False, {Duration:}-1)
    Task.PlayAnim({Char:}-1, {Anim Name:}"PANIC_LOOP", {Anim File:}"ON_LOOKERS", {Speed:}4.0, {Loop:}False, {Lock X:}False, {Lock Y:}False, {Freeze Frame:}False, {Duration:}-1)
    Task.PlayAnim({Char:}-1, {Anim Name:}"PANIC_POINT", {Anim File:}"ON_LOOKERS", {Speed:}4.0, {Loop:}False, {Lock X:}False, {Lock Y:}False, {Freeze Frame:}False, {Duration:}-1)
    Task.PlayAnim({Char:}-1, {Anim Name:}"PANIC_LOOP", {Anim File:}"ON_LOOKERS", {Speed:}4.0, {Loop:}False, {Lock X:}False, {Lock Y:}False, {Freeze Frame:}False, {Duration:}-1)
Sequence.Close(seqAnimPoint)
Audio.PlayMissionAudio(MissionAudioSlot.Slot2) // Male 2: What in tarnation?!
Char.PerformSequence({Char:}cIntroMale2, {Sequence:}seqAnimPoint)
wait 0
Sequence.Clear(seqAnimPoint)
wait 1000
Camera.SetFixedPosition({Pos3:}-713.672913, -1849.339844, 16.056463, {Offset Pos3:}0.0, 0.0, 0.0)
Camera.PointAtPoint({Pos3:}-839.196655, -2067.679443, -57.418488, SwitchType.JumpCut)
Task.TurnCharToFaceCoord(cIntroMale1, -725.0519,  -1882.1888, 6.0388)
Task.TurnCharToFaceCoord(cIntroFemale1, -725.0519,  -1882.1888, 6.0388)
Task.TurnCharToFaceCoord(cIntroFemale2, -725.0519,  -1882.1888, 6.0388)
wait 2000
asRoar.SetState(AudioStreamAction.Play)
wait 500
Camera.DoFade(0, Fade.Out)

Char.Delete(cIntroMale1)
Char.Delete(cIntroMale2)
Char.Delete(cIntroFemale1)
Char.Delete(cIntroFemale2)
Car.Delete(vIntroCar)
wait 5000


//--- Part 3.1: The Bar: Exterior Shot
//    CJ is at a bar enjoying his day off. We see his car outside with the text 'Two Weeks Later'...
//------------------------------------------------------------------------------------------------------
:Mission_Introduction_Scene3
skip_cutscene_end
Text.ClearPrints()
Streaming.MarkModelAsNoLongerNeeded(#DWMYLC1) // Cowboy hat male
Streaming.MarkModelAsNoLongerNeeded(#CWFYHB)  // Country Female
Streaming.MarkModelAsNoLongerNeeded(#CWMYFR)  // Country Male
Streaming.MarkModelAsNoLongerNeeded(#DNFYLC)  // Country female - Eve's Mom
Streaming.MarkModelAsNoLongerNeeded(#SADLER)  // Family car
Streaming.RemoveAnimation("ON_LOOKERS")
skip_cutscene_start_internal @Mission_Introduction_Scene4
Clock.SetTimeOfDay(21, 0)

// Stream area. Put player in position.
Char.SetCoordinates($scplayer, {Pos3:}2510.2959, 1202.2839, 10.8203)
Streaming.RequestCollision({Pos2:}2510.2959, 1202.2839)
Streaming.RequestModel(#INFERNUS)
Streaming.LoadAllModelsNow()
wait 0
Char.SetCoordinates({Pos3:} $scplayer, 2510.2959, 1202.2839, -100.0)

// Spawn CJ's car outside the club.
Car.CustomPlateForNextCar(#INFERNUS, "XFILES4")
Car vIntroInfernus = Car.Create(#INFERNUS, {Pos3:}2497.2319, 1247.0275, 10.8203)
Car.SetHeading(vIntroInfernus, 270.0)
Car.ChangeColor(vIntroInfernus, 0, 0)
wait 0
Car.MarkAsNoLongerNeeded(vIntroInfernus)

// Camera tracks down to face the club. "Two Weeks Later..." displays.
Camera.SetVectorMove({From Pos3:}2446.416504, 1301.942505, 29.584843, {To Pos3:}2495.787598, 1258.685791, 13.387424, {Duration:}10000, {Ease:}False)
Camera.SetVectorTrack({To Pos3:}2468.504395, 1275.212402, 29.296982, {To Pos3:}2546.237061, 1168.128540, 21.925793, {Duration:}10000, {Ease:}False)
Camera.DoFade(5000, Fade.In)
Text.PrintBig("XSM0103", 5000, TextStyle.Middle)
wait 9000
Camera.DoFade(1000, Fade.Out)


//--- Part 3.2: The Bar: Interior
//    CJ is at a bar enjoying his day off. We see his car outside with the text 'Two Weeks Later'...
//------------------------------------------------------------------------------------------------------
:Mission_Introduction_Scene4 // Bar Interior
skip_cutscene_end
Text.ClearThisPrintBigNow(TextStyle.Middle)
Text.ClearPrints()
wait 1000
Camera.DoFade(0, Fade.Out)
skip_cutscene_start_internal @Mission_Introduction_Cleanup

// Load models and bar area.
Streaming.RequestAnimation("BAR")
Streaming.RequestAnimation("DANCING")
Streaming.RequestAnimation("RUNNINGMAN")
Streaming.RequestAnimation("GANGS")
Streaming.RequestAnimation("MISC")
Streaming.RequestModel(#WMYVA) // Bartender
Streaming.RequestModel(#DNMOLC1)
Streaming.RequestModel(#SWFYRI)
Streaming.RequestModel(#CELLPHONE)
//-
Char.SetAreaVisible($scplayer, 17)
Char.SetCoordinates($scplayer, 499.9697, -20.6059, 1000.6)
Streaming.SetAreaVisible(17) // Bar
Streaming.RequestCollision(499.9697, -20.6059)
Streaming.LoadAllModelsNow()

// Define anim sequences.
Sequence animBartender = Sequence.Open()
    Task.PlayAnim({Char:}-1, {Anim Name:}"barserve_bottle", {Anim File:}"bar", {Speed:}4.0, {Loop:}False, {LockX:}False, {LockY:}False, {Freeze Frame:}False, {Duration:}-1)
    Task.PlayAnim({Char:}-1, {Anim Name:}"barserve_in",     {Anim File:}"bar", {Speed:}4.0, {Loop:}False, {LockX:}False, {LockY:}False, {Freeze Frame:}False, {Duration:}-1)
    Task.PlayAnim({Char:}-1, {Anim Name:}"barserve_loop",   {Anim File:}"bar", {Speed:}4.0, {Loop:}False, {LockX:}False, {LockY:}False, {Freeze Frame:}False, {Duration:}-1)
Sequence.Close(animBartender)

Sequence animDanceEpicA1 = Sequence.Open()
    Task.PlayAnim({Char:}-1, {Anim Name:}"dance_b1",  {Anim File:}"runningman", {Speed:}4.0, {Loop:}False, {LockX:}False, {LockY:}False, {Freeze Frame:}False, {Duration:}-1)
    Task.PlayAnim({Char:}-1, {Anim Name:}"dance_b2",  {Anim File:}"runningman", {Speed:}4.0, {Loop:}False, {LockX:}False, {LockY:}False, {Freeze Frame:}False, {Duration:}-1)
    Task.PlayAnim({Char:}-1, {Anim Name:}"dance_b3",  {Anim File:}"runningman", {Speed:}4.0, {Loop:}False, {LockX:}False, {LockY:}False, {Freeze Frame:}False, {Duration:}-1)
    Task.PlayAnim({Char:}-1, {Anim Name:}"dance_b4",  {Anim File:}"runningman", {Speed:}4.0, {Loop:}False, {LockX:}False, {LockY:}False, {Freeze Frame:}False, {Duration:}-1)
    Task.PlayAnim({Char:}-1, {Anim Name:}"dance_b5",  {Anim File:}"runningman", {Speed:}4.0, {Loop:}False, {LockX:}False, {LockY:}False, {Freeze Frame:}False, {Duration:}-1)
    Task.PlayAnim({Char:}-1, {Anim Name:}"dance_b6",  {Anim File:}"runningman", {Speed:}4.0, {Loop:}False, {LockX:}False, {LockY:}False, {Freeze Frame:}False, {Duration:}-1)
    Task.PlayAnim({Char:}-1, {Anim Name:}"dance_b7",  {Anim File:}"runningman", {Speed:}4.0, {Loop:}False, {LockX:}False, {LockY:}False, {Freeze Frame:}False, {Duration:}-1)
    Task.PlayAnim({Char:}-1, {Anim Name:}"dance_b8",  {Anim File:}"runningman", {Speed:}4.0, {Loop:}False, {LockX:}False, {LockY:}False, {Freeze Frame:}False, {Duration:}-1)    
Sequence.Close(animDanceEpicA1)
Sequence animDanceEpicA2 = Sequence.Open()
    Task.PlayAnim({Char:}-1, {Anim Name:}"dance_b9",  {Anim File:}"runningman", {Speed:}4.0, {Loop:}False, {LockX:}False, {LockY:}False, {Freeze Frame:}False, {Duration:}-1)
    Task.PlayAnim({Char:}-1, {Anim Name:}"dance_b10", {Anim File:}"runningman", {Speed:}4.0, {Loop:}False, {LockX:}False, {LockY:}False, {Freeze Frame:}False, {Duration:}-1)
    Task.PlayAnim({Char:}-1, {Anim Name:}"dance_b11", {Anim File:}"runningman", {Speed:}4.0, {Loop:}False, {LockX:}False, {LockY:}False, {Freeze Frame:}False, {Duration:}-1)
    Task.PlayAnim({Char:}-1, {Anim Name:}"dance_b12", {Anim File:}"runningman", {Speed:}4.0, {Loop:}False, {LockX:}False, {LockY:}False, {Freeze Frame:}False, {Duration:}-1)
    Task.PlayAnim({Char:}-1, {Anim Name:}"dance_b13", {Anim File:}"runningman", {Speed:}4.0, {Loop:}False, {LockX:}False, {LockY:}False, {Freeze Frame:}False, {Duration:}-1)
    Task.PlayAnim({Char:}-1, {Anim Name:}"dance_b14", {Anim File:}"runningman", {Speed:}4.0, {Loop:}False, {LockX:}False, {LockY:}False, {Freeze Frame:}False, {Duration:}-1)
    Task.PlayAnim({Char:}-1, {Anim Name:}"dance_b15", {Anim File:}"runningman", {Speed:}4.0, {Loop:}False, {LockX:}False, {LockY:}False, {Freeze Frame:}False, {Duration:}-1)
    Task.PlayAnim({Char:}-1, {Anim Name:}"dance_b16", {Anim File:}"runningman", {Speed:}4.0, {Loop:}False, {LockX:}False, {LockY:}False, {Freeze Frame:}False, {Duration:}-1)
Sequence.Close(animDanceEpicA2)

Sequence animDanceEpicB1 = Sequence.Open()
    Task.PlayAnim({Char:}-1, {Anim Name:}"dance_g1",  {Anim File:}"runningman", {Speed:}4.0, {Loop:}False, {LockX:}False, {LockY:}False, {Freeze Frame:}False, {Duration:}-1)
    Task.PlayAnim({Char:}-1, {Anim Name:}"dance_g2",  {Anim File:}"runningman", {Speed:}4.0, {Loop:}False, {LockX:}False, {LockY:}False, {Freeze Frame:}False, {Duration:}-1)
    Task.PlayAnim({Char:}-1, {Anim Name:}"dance_g3",  {Anim File:}"runningman", {Speed:}4.0, {Loop:}False, {LockX:}False, {LockY:}False, {Freeze Frame:}False, {Duration:}-1)
    Task.PlayAnim({Char:}-1, {Anim Name:}"dance_g4",  {Anim File:}"runningman", {Speed:}4.0, {Loop:}False, {LockX:}False, {LockY:}False, {Freeze Frame:}False, {Duration:}-1)
    Task.PlayAnim({Char:}-1, {Anim Name:}"dance_g5",  {Anim File:}"runningman", {Speed:}4.0, {Loop:}False, {LockX:}False, {LockY:}False, {Freeze Frame:}False, {Duration:}-1)
    Task.PlayAnim({Char:}-1, {Anim Name:}"dance_g6",  {Anim File:}"runningman", {Speed:}4.0, {Loop:}False, {LockX:}False, {LockY:}False, {Freeze Frame:}False, {Duration:}-1)
    Task.PlayAnim({Char:}-1, {Anim Name:}"dance_g7",  {Anim File:}"runningman", {Speed:}4.0, {Loop:}False, {LockX:}False, {LockY:}False, {Freeze Frame:}False, {Duration:}-1)
    Task.PlayAnim({Char:}-1, {Anim Name:}"dance_g8",  {Anim File:}"runningman", {Speed:}4.0, {Loop:}False, {LockX:}False, {LockY:}False, {Freeze Frame:}False, {Duration:}-1)
Sequence.Close(animDanceEpicB1)
Sequence animDanceEpicB2 = Sequence.Open()
    Task.PlayAnim({Char:}-1, {Anim Name:}"dance_g9",  {Anim File:}"runningman", {Speed:}4.0, {Loop:}False, {LockX:}False, {LockY:}False, {Freeze Frame:}False, {Duration:}-1)
    Task.PlayAnim({Char:}-1, {Anim Name:}"dance_g10", {Anim File:}"runningman", {Speed:}4.0, {Loop:}False, {LockX:}False, {LockY:}False, {Freeze Frame:}False, {Duration:}-1)
    Task.PlayAnim({Char:}-1, {Anim Name:}"dance_g11", {Anim File:}"runningman", {Speed:}4.0, {Loop:}False, {LockX:}False, {LockY:}False, {Freeze Frame:}False, {Duration:}-1)
    Task.PlayAnim({Char:}-1, {Anim Name:}"dance_g12", {Anim File:}"runningman", {Speed:}4.0, {Loop:}False, {LockX:}False, {LockY:}False, {Freeze Frame:}False, {Duration:}-1)
    Task.PlayAnim({Char:}-1, {Anim Name:}"dance_g13", {Anim File:}"runningman", {Speed:}4.0, {Loop:}False, {LockX:}False, {LockY:}False, {Freeze Frame:}False, {Duration:}-1)
    Task.PlayAnim({Char:}-1, {Anim Name:}"dance_g14", {Anim File:}"runningman", {Speed:}4.0, {Loop:}False, {LockX:}False, {LockY:}False, {Freeze Frame:}False, {Duration:}-1)
    Task.PlayAnim({Char:}-1, {Anim Name:}"dance_g15", {Anim File:}"runningman", {Speed:}4.0, {Loop:}False, {LockX:}False, {LockY:}False, {Freeze Frame:}False, {Duration:}-1)
    Task.PlayAnim({Char:}-1, {Anim Name:}"dance_g16", {Anim File:}"runningman", {Speed:}4.0, {Loop:}False, {LockX:}False, {LockY:}False, {Freeze Frame:}False, {Duration:}-1)
Sequence.Close(animDanceEpicB2)

// Put player in position. Give them a booze bottle.
Char.SetCoordinates($scplayer, 499.9697, -20.6059, 1000.6168)
Char.SetHeading($scplayer, 270.0)
Object oBottle = Object.Create(1484, 0.0, 0.0, 0.0)
Object.SetAreaVisible(oBottle, 17)
Task.PickUpObject({Char:}$scplayer, {Object:}oBottle, {Offset Pos3:}0.0, 0.0, 0.0, {BoneID:}6, {Unknown}16, {Anim Name:}"NULL", {Anim File:}"NULL", {Duration:}-1)                  

// Create scene characters
Char cIntroBartender = Char.Create(PedType.Special, #WMYVA, {Pos3:}501.8349, -18.6194, 1000.6)
Char.SetHeading(cIntroBartender, 86.0)
Char.SetAreaVisible(cIntroBartender, 17)
Char.PerformSequence(cIntroBartender, animBartender)
wait 0
Sequence.Clear(animBartender)

Char cIntroEpicDancerA = Char.Create(PedType.Special, #DNMOLC1, {Pos3:}487.8065, -15.0321, 1000.6)
Char.SetHeading(cIntroEpicDancerA, 0.0)
Char.SetAreaVisible(cIntroEpicDancerA, 17)

Char cIntroEpicDancerB = Char.Create(PedType.Special, #SWFYRI, {Pos3:}487.6528, -14.023, 1000.6797)
Char.SetHeading(cIntroEpicDancerB, 173.0)
Char.SetAreaVisible(cIntroEpicDancerA, 17)


// Camera behind-left of CJ, facing towards bar corner
Camera.SetFixedPosition({Pos3:}495.2984, -16.1996, 1000.2106, {Offset Pos3:}0.0, 0.0, 0.0)
Camera.PointAtPoint({Pos3:}502.326, -23.1769, 1001.4701, SwitchType.JumpCut)
wait 3000
Camera.DoFade(1000, Fade.In)
wait 1000

var cArrayIntroDancers: array 6 of Char
int cArrayIntroDancersIndex = 0
Char.CreateRandom(490.1774, -21.2311, 1000.6797, cArrayIntroDancers[cArrayIntroDancersIndex])
Char.SetHeading(cArrayIntroDancers[cArrayIntroDancersIndex], 17.4743)
cArrayIntroDancersIndex += 1
Char.CreateRandom(482.7841, -18.0025, 1000.6797, cArrayIntroDancers[cArrayIntroDancersIndex])
Char.SetHeading(cArrayIntroDancers[cArrayIntroDancersIndex], 306.057)
cArrayIntroDancersIndex += 1
Char.CreateRandom(482.7142, -12.7037, 1000.6797, cArrayIntroDancers[cArrayIntroDancersIndex])
Char.SetHeading(cArrayIntroDancers[cArrayIntroDancersIndex], 246.2098)
cArrayIntroDancersIndex += 1
Char.CreateRandom(481.9807, -10.0222,  1000.6797, cArrayIntroDancers[cArrayIntroDancersIndex])
Char.SetHeading(cArrayIntroDancers[cArrayIntroDancersIndex], 222.3963)
cArrayIntroDancersIndex += 1
Char.CreateRandom(485.569,  -7.8098,  1000.6719, cArrayIntroDancers[cArrayIntroDancersIndex])
Char.SetHeading(cArrayIntroDancers[cArrayIntroDancersIndex], 268.7701)
cArrayIntroDancersIndex += 1
Char.CreateRandom(481.9807, -8.0222,  1000.6797, cArrayIntroDancers[cArrayIntroDancersIndex])
Char.SetHeading(cArrayIntroDancers[cArrayIntroDancersIndex], 222.3963)
cArrayIntroDancersIndex = 0
repeat
    wait 100
    Task.PlayAnim({Char:}cArrayIntroDancers[cArrayIntroDancersIndex], {Anim Name:}"dance_loop", {Anim File:}"dancing", {Speed:}4.0, {Loop:}True, {LockX:}False, {LockY:}False, {Freeze Frame:}False, {Duration:}-1)
    cArrayIntroDancersIndex += 1
until cArrayIntroDancersIndex >= 6

Task.PlayAnim({Char:} $scplayer, {Anim Name:}"drnkbr_prtl", {Anim File:}"GANGS", {Speed:}4.0, {Loop:}False, {LockX:}False, {LockY:}False, {Freeze Frame:}False, {Duration:}2000)
wait 2000

// Camera: Close-up on epic dancing duo
Camera.SetFixedPosition({Pos3:}490.0669, -16.97, 1000.6797, {Offset Pos3:}0.0, 0.0, 0.0)
Camera.PointAtPoint({Pos3:}484.8581, -12.8829, 1001.3391, SwitchType.JumpCut)
Char.PerformSequence(cIntroEpicDancerA, animDanceEpicA1)
Char.PerformSequence(cIntroEpicDancerB, animDanceEpicB1)
wait 0
Sequence.Clear(animDanceEpicA1)
Sequence.Clear(animDanceEpicA1)
Task.TurnCharToFaceChar($scplayer, cIntroEpicDancerA)
wait 3000

// Camera: Close to CJ, facing bar
Camera.SetFixedPosition({Pos3:}497.6537, -17.303, 1001.0566, {Offset Pos3:}0.0, 0.0, 0.0)
Camera.PointAtPoint({Pos3:}502.7314, -19.4638, 1000.8315, SwitchType.JumpCut)
Task.TurnCharToFaceChar($scplayer, cIntroBartender)
wait 250
Task.PlayAnim({Char:} $scplayer, {Anim Name:}"drnkbr_prtl", {Anim File:}"GANGS", {Speed:}4.0, {Loop:}False, {LockX:}False, {LockY:}False, {Freeze Frame:}False, {Duration:}3000)
wait 3000
Task.PlayAnim({Char:} $scplayer, {Anim Name:}"idle_chat", {Anim File:}"ped", {Speed:}4.0, {Loop:}False, {LockX:}False, {LockY:}False, {Freeze Frame:}False, {Duration:}2500)
Text.PrintNow("XSM0104", 2500, 1) // ~y~CJ: ~w~Hey bartender, can I get another one?
wait 1000
Task.TurnCharToFaceChar(cIntroBartender, $scplayer)
wait 1500
Text.PrintNow("XSM0105", 2500, 1) // ~y~Bartender:~w~ Coming right up, CJ.
Task.PlayAnim({Char:}cIntroBartender, {Anim Name:}"idle_chat", {Anim File:}"ped", {Speed:}4.0, {Loop:}False, {LockX:}False, {LockY:}False, {Freeze Frame:}False, {Duration:}2500)
wait 2500
Task.PlayAnim({Char:}cIntroBartender, {Anim Name:}"barserve_loop", {Anim File:}"bar", {Speed:}4.0, {Loop:}False, {LockX:}False, {LockY:}False, {Freeze Frame:}False, {Duration:}-1)

// Camera: Close-up on epic dancing duo
Camera.SetFixedPosition({Pos3:}490.0669, -16.97, 1000.6797, {Offset Pos3:}0.0, 0.0, 0.0)
Camera.PointAtPoint(484.8581, -12.8829, 1001.3391, SwitchType.JumpCut)
Object.Delete(oBottle)
Char.PerformSequence(cIntroEpicDancerA, animDanceEpicA2)
Char.PerformSequence(cIntroEpicDancerB, animDanceEpicB2)
wait 0
Sequence.Clear(animDanceEpicA2)
Sequence.Clear(animDanceEpicB2)
Task.TurnCharToFaceChar($scplayer, cIntroEpicDancerA)
Audio.LoadMissionAudio(MissionAudioSlot.Slot1, 23000)
wait 3000
while not Audio.HasMissionAudioLoaded(MissionAudioSlot.Slot1)
    wait 0
end

// Camera: Close to CJ, facing bar
Camera.SetFixedPosition({Pos3:}498.5305, -20.0609, 1001.0, {Offset Pos3:}0.0, 0.0, 0.0)
Camera.PointAtPoint(500.5666, -21.1248, 1001.0, SwitchType.JumpCut)
wait 500
Audio.PlayMissionAudio(MissionAudioSlot.Slot1)
wait 2000
Task.UseMobilePhone($scplayer, True)
wait 500

// Camera: Close to CJ, facing bar
Camera.SetFixedPosition({Pos3:}497.0972, -20.7016, 1000.7, {Offset Pos3:}0.0, 0.0, 0.0)
Camera.PointAtPoint({Pos3:}501.132, -20.7632, 1000.7, SwitchType.JumpCut)
Char.SetHeading($scplayer, 93.0)
wait 2000
Audio.LoadMissionAudio(MissionAudioSlot.Slot1, 12814)
while not Audio.HasMissionAudioLoaded(MissionAudioSlot.Slot1)
    wait 0
end
Audio.PlayMissionAudio(MissionAudioSlot.Slot1)
repeat
    wait 500
until Audio.HasMissionAudioFinished(MissionAudioSlot.Slot1)
// XSM0107 ~y~?: ~w~This is an automated message. Directive 665 has been issued. All Prospero agents are to attend Prospero H.Q. immediately. Message repeating.
AudioStream asDialogue = AudioStream.Load("modloader\a X-Files IV\audio\dialogue\introduction\XSM0107.mp3")
repeat
    wait 0
until AudioStream.Load("modloader\a X-Files IV\audio\dialogue\introduction\XSM0107.mp3", asDialogue)
AudioStream.SetState(asDialogue, AudioStreamAction.Play)
repeat
    wait 0
until not AudioStream.IsPlaying(asDialogue)
AudioStream.SetState(asDialogue, AudioStreamAction.Play)
wait 1000
AudioStream.SetState(asDialogue, AudioStreamAction.Stop)
func_playSoundSafely({slotID:} 1, {soundID:} 4203, {waitUntilFinished:} True)
wait 1000
Task.PlayAnim({Char:} $scplayer, {Anim Name:}"plyr_shkhead", {Anim File:}"misc", {Speed:}4.0, {Loop:}False, {LockX:}False, {LockY:}False, {Freeze Frame:}False, {Duration:}-1)
wait 2000
// XSM0108 ~y~CJ: ~w~Well, so much for my day off...
func_playDialogue({mode:}0, {asPath:}"modloader\a X-Files IV\audio\dialogue\introduction\XSM0108.mp3", {scmID:}-1, {subID:}"XSM0108", {doAnims:}True, $scplayer, {waitToFinish:}True, {extraWaitTime:}500)
wait 1000
Task.PlayAnim({Char:} $scplayer, {Anim Name:}"phone_out", {Anim File:}"ped", {Speed:}4.0, {Loop:}False, {LockX:}False, {LockY:}False, {Freeze Frame:}False, {Duration:}-1)
wait 1000
repeat
    wait 0
until not Char.IsPlayingAnim($scplayer, "phone_out")
Task.UseMobilePhone($scplayer, False)
Char.SetCurrentWeapon($scplayer, WeaponType.Unarmed)
Task.CharSlideToCoord($scplayer, {Pos3:}495.0032, -22.45, 1000.6797, {Stop Angle:}115.5602, {Stop Radius:}0.1)
Camera.SetFixedPosition({Pos3:}494.145569, -22.772722, 1000.485291, {Offset Pos3:}0.0, 0.0, 0.0)
Camera.PointAtPoint({Pos3:}1445.754639, 185.501373, 1217.822754, SwitchType.JumpCut)
Camera.DoFade(3000, Fade.Out)
wait 5000


// Mission cleanup.
:Mission_Introduction_Cleanup
skip_cutscene_end
Streaming.MarkModelAsNoLongerNeeded(#INFERNUS)
Streaming.MarkModelAsNoLongerNeeded(#WMYVA) // Bartender
Streaming.MarkModelAsNoLongerNeeded(#DNMOLC1)
Streaming.MarkModelAsNoLongerNeeded(#SWFYRI)
Streaming.MarkModelAsNoLongerNeeded(#CELLPHONE)
Streaming.RemoveAnimation("BAR")
Streaming.RemoveAnimation("DANCING")
Streaming.RemoveAnimation("RUNNINGMAN")
Streaming.RemoveAnimation("GANGS")
Streaming.RemoveAnimation("MISC")
AudioStream.Remove(asRoar)
Text.ClearPrints()
$flag_Mission_Introduction_Completed = True
$flag_Player_On_Mission = False
Mission.Finish()
terminate_this_script